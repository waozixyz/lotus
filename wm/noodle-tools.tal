( @|Brush )

@brush/<paint-rect> ( -- )
	.pointer/y1 LDZ2 .pointer/y0 LDZ2 GTH2k [ JMP SWP2 ]
	&>ver
		STH2k .pointer/x1 LDZ2 .pointer/x0 LDZ2 GTH2k [ JMP SWP2 ]
		&>hor
			DUP2 STH2kr /<apply>
			INC2 gts2k ?&>hor
		POP2 POP2 POP2r INC2 gts2k ?&>ver
	POP2 POP2 canvas/<draw> !panel/<draw>

%brush/outside ( x y -- t ) {
	#00 SWP ;brush/size LDZ #30 SFT2 ;size-icns ADD2 ADD2 LDA #ff EOR SWP SFT #01 AND }

@brush/<paint-dot> ( x* y* -- )
	#0003 SUB2 ,&y STR2
	#0004 SUB2 ,&x STR2
	#0800
	&>verd
		STHk #0800
		&>hord
			DUP STHkr brush/outside ?{
				( x ) #00 OVR [ LIT2 &x $2 ] ADD2
				( y ) #00 STHkr [ LIT2 &y $2 ] ADD2 /<apply> }
			INC GTHk ?&>hord
		POP2 POPr INC GTHk ?&>verd
	POP2 JMP2r

@brush/<paint-magic> ( x* y* -- )
	,&ym STR2
	,&xm STR2
	#0700
	&>verm
		STHk #0700
		&>horm
			( x ) #00 OVR [ LIT2 &xm $2 ] ADD2
			( y ) #00 STHkr [ LIT2 &ym $2 ] ADD2 canvas/<magic-pixel>
			INC GTHk ?&>horm
		POP2 POPr INC GTHk ?&>verm
	POP2 JMP2r

@brush/<apply> ( x* y* -- )
	ROTk ROT POP .&patt-fn LDZ2 JSR2 ?{ POP2 POP2 JMP2r }
	.&oper LDZ2 JMP2

@brush/get-tool ( -- ptr* )
	/<set-oper>
	( touch ) ;Mouse/state DEI #00 NEQ DUP ADD
	( changed ) [ LIT2 00 &last $1 ] NEQ ADD DUP ADD
	( lut ) .&tool LDZ #30 SFT ADD ;&tools-lut ADD2 LDA2
	( record ) .Mouse/state DEI ,&last STR
	JMP2r

@brush/<set-oper> ( -- )
	.&mode LDZ ?{
		[ LIT2 01 -Mouse/state ] DEI LTH ?{ ;canvas/add-pixel .&oper STZ2
			JMP2r } }
	;canvas/remove-pixel .&oper STZ2
	JMP2r

@brush/<set-tool> ( tool -- )
	.&tool STZ !panel/<draw-toolpane>
	&<set-tool-pen> #00 !/<set-tool>
	&<set-tool-brush> #01 !/<set-tool>
	&<set-tool-line> #02 !/<set-tool>
	&<set-tool-magic> #03 !/<set-tool>
	&<set-tool-rect> #04 !/<set-tool>
	&<set-tool-zoom> #05 !/<set-tool>

@brush/<set-patt> ( patt -- )
	#07 AND
	( set fn ) #00 OVR DUP ADD ;patternize/lut ADD2 LDA2 .&patt-fn STZ2
	.&patt STZ
	( | select brush )
	.&tool LDZ ?{ #01 /<set-tool> }
	!panel/<draw-pattpane>
	&<set-patt1> #00 !/<set-patt>
	&<set-patt2> #01 !/<set-patt>
	&<set-patt3> #02 !/<set-patt>
	&<set-patt4> #03 !/<set-patt>
	&<set-patt5> #04 !/<set-patt>
	&<set-patt6> #05 !/<set-patt>
	&<set-patt7> #06 !/<set-patt>
	&<set-patt8> #07 !/<set-patt>

@brush/<decr-size> ( -- )
	.&size LDZ #01 SUB !/<set-size>

@brush/<incr-size> ( -- )
	.&size LDZ INC
	( >> )

@brush/<set-size> ( size -- )
	#07 AND
	( | toggle mode on reselection )
	DUP .&size LDZ NEQ ?{
		.&mode LDZk #00 EQU SWP STZ }
	.&size STZ
	( | select brush )
	.&tool LDZ ?{ #01 /<set-tool> }
	!panel/<draw-sizepane>

@brush/<toggle-erase> ( -- )
	.&mode LDZk #00 EQU SWP STZ !panel/<draw-sizepane>

@brush/tools-lut [
	=pencil/hover =pencil/up
	=pencil/down =pencil/drag
	=marker/hover =marker/up
	=marker/down =marker/drag
	=stroke/hover =stroke/up
	=stroke/down =stroke/drag
	=magic/hover =magic/up
	=magic/down =magic/drag
	=box/hover =box/up
	=box/down =box/drag
	=magnify/hover =magnify/up
	=magnify/down =magnify/drag ]

@patternize/full ( x y -- c )
	POP2 #01 JMP2r

@patternize/a ( x y -- c )
	ADD #01 AND #00 EQU JMP2r

@patternize/b ( x y -- c )
	#0303 AND2 ADDk STH
	#0202 ADD2 #0303 AND2 ADD #00 EQU STHr #00 EQU ORA JMP2r

@patternize/c ( x y -- c )
	#0707 AND2 ADDk STH
	#0404 ADD2 #0707 AND2 ADD #00 EQU STHr #00 EQU ORA JMP2r

@patternize/diag-r ( x y -- c )
	SUB #03 AND #00 EQU JMP2r

@patternize/diag-l ( x y -- c )
	ADD #03 AND #00 EQU JMP2r

@patternize/ver ( x y -- c )
	NIP #01 AND JMP2r

@patternize/hor ( x y -- c )
	POP #01 AND JMP2r

@patternize/lut [
	=&full =&a =&b =&c
	=&diag-r =&diag-l =&ver =&hor ]

@guide/do-draw ( x* y* -- )
	.Screen/y DEO2
	.Screen/x DEO2
	[ LIT2 42 -Screen/pixel ] DEO
	JMP2r

@guide/do-clear ( x* y* -- )
	.Screen/y DEO2
	.Screen/x DEO2
	[ LIT2 40 -Screen/pixel ] DEO
	JMP2r

@guide/<draw-line> ( -- )
	.zoom/active LDZ ?{
		.&x LDZ2 .&y LDZ2 .&x2 LDZ2 .&y2 LDZ2 ;&do-draw !line/<paint> }
	JMP2r

@guide/<clear-line> ( -- )
	.zoom/active LDZ ?{
		.&x LDZ2 .&y LDZ2 .&x2 LDZ2 .&y2 LDZ2 ;&do-clear !line/<paint> }
	JMP2r

@guide/<clear-rect> ( -- )
	.zoom/active LDZ ?{ #40 !guide/<line-rect> }
	JMP2r

@guide/<draw-rect> ( -- )
	.zoom/active LDZ ?{ #42 !guide/<line-rect> }
	JMP2r

@guide/<line-rect> ( color -- )
	STH
	[ LITr -Screen/pixel ]
	( | hor )
	.&x2 LDZ2 .&x LDZ2 GTH2k [ JMP SWP2 ]
	&>hor
		DUP2 .Screen/x DEO2
		.&y LDZ2 .Screen/y DEO2
		DEOkr
		.&y2 LDZ2 .Screen/y DEO2
		DEOkr
		INC2 GTH2k ?&>hor
	POP2 POP2
	( | ver )
	.&y2 LDZ2 INC2 .&y LDZ2 GTH2k [ JMP SWP2 ]
	&>ver
		DUP2 .Screen/y DEO2
		.&x LDZ2 .Screen/x DEO2
		DEOkr
		.&x2 LDZ2 .Screen/x DEO2
		DEOkr
		INC2 GTH2k ?&>ver
	POP2 POP2 POP2r JMP2r

(
@|Tools )

@pencil/down ( x* y* -> )
	SWP2k .pointer/x0 STZ2
	.pointer/y0 STZ2
	OVR2 OVR2 .brush/oper LDZ2 JSR2
	( >> )

@pencil/up ( x* y* -> )
	canvas/<request>
	filestate/<change>
	BRK

@pencil/drag ( x* y* -> )
	OVR2 OVR2 STH2
	STH2
	.pointer/x0 LDZ2 .pointer/y0 LDZ2 STH2r STH2r .brush/oper LDZ2 line/<paint>
	.pointer/y0 STZ2
	.pointer/x0 STZ2
	BRK

@pencil/hover ( x* y* -> )
	POP2 POP2 BRK

@marker/down ( x* y* -> )
	SWP2k .pointer/x0 STZ2
	.pointer/y0 STZ2
	OVR2 OVR2 brush/<paint-dot>
	( >> )

@marker/up ( x* y* -> )
	canvas/<request>
	filestate/<change>
	BRK

@marker/drag ( x* y* -> )
	OVR2 OVR2 STH2
	STH2
	.pointer/x0 LDZ2 .pointer/y0 LDZ2 STH2r STH2r ;brush/<paint-dot>
	line/<paint>
	.pointer/y0 STZ2
	.pointer/x0 STZ2
	BRK

@marker/hover ( x* y* -> )
	POP2 POP2 BRK

@stroke/down ( x* y* -> )
	cursor/<clear>
	guide/<clear-line>
	.pointer/y0 STZ2
	.pointer/x0 STZ2
	BRK

@stroke/up ( x* y* -> )
	cursor/<clear>
	guide/<clear-line>
	.pointer/y1 STZ2
	.pointer/x1 STZ2
	.pointer/x0 LDZ2 .pointer/y0 LDZ2 .pointer/x1 LDZ2 .pointer/y1 LDZ2 ;brush/<paint-dot>
	line/<paint>
	filestate/<change>
	BRK

@stroke/drag ( x* y* -> )
	cursor/<clear>
	guide/<clear-line>
	.pointer/y1 STZ2
	.pointer/x1 STZ2
	.pointer/x0 LDZ2 .pointer/y0 LDZ2 canvas/offset-pos .guide/y STZ2
	.guide/x STZ2
	.pointer/x1 LDZ2 .pointer/y1 LDZ2 canvas/offset-pos .guide/y2 STZ2
	.guide/x2 STZ2
	guide/<draw-line>
	BRK

@stroke/hover ( x* y* -> )
	POP2 POP2 BRK

@magic/up ( x* y* -> )
	filestate/<change>
	( >> )

@magic/down ( x* y* -> )
	( >> )

@magic/drag ( x* y* -> )
	#0003 SUB2 SWP2 #0003 SUB2 SWP2 brush/<paint-magic>
	BRK

@magic/hover ( x* y* -> )
	POP2 POP2 BRK

@box/down ( x* y* -> )
	cursor/<clear>
	guide/<clear-rect>
	.pointer/y0 STZ2
	.pointer/x0 STZ2
	BRK

@box/up ( x* y* -> )
	cursor/<clear>
	guide/<clear-rect>
	.pointer/y1 STZ2
	.pointer/x1 STZ2
	brush/<paint-rect>
	filestate/<change>
	BRK

@box/drag ( x* y* -> )
	cursor/<clear>
	guide/<clear-rect>
	.pointer/y1 STZ2
	.pointer/x1 STZ2
	.pointer/x0 LDZ2 .pointer/y0 LDZ2 canvas/offset-pos .guide/y STZ2
	.guide/x STZ2
	.pointer/x1 LDZ2 .pointer/y1 LDZ2 canvas/offset-pos .guide/y2 STZ2
	.guide/x2 STZ2
	guide/<draw-rect>
	BRK

@box/hover ( x* y* -> )
	POP2 POP2 BRK

@magnify/up ( x* y* -> )
	;window/rows LDZ #01 SFT2 SUB2 .zoom/y STZ2
	;window/cols LDZ #01 SFT2 SUB2 .zoom/x STZ2
	zoom/<toggle>
	BRK

@magnify/hover ( x* y* -> )
	( >> )

@magnify/down ( x* y* -> )
	( >> )

@magnify/drag ( x* y* -> )
	POP2 POP2 BRK

(
@|tga )

@tga/<load> ( -- )
	;filepath/buf .File/name DEO2
	#0012 .File/length DEO2
	;image .File/read DEO2
	( | rename )
	filepath/<pop-ext>
	;&icn-ext filepath/<push-str>
	( | flip endianness )
	;image/w LDA2k SWP SWP2 STA2
	;image/h LDA2k SWP SWP2 STA2
	( | resize canvas )
	;image/w LDA2 #03 SFT2 NIP ;image/h LDA2 #03 SFT2 NIP canvas/<set-size>
	( | get parser )
	;image/image-type LDA #03 NEQ ?{ !tga/<parse-bw> }
	;&error-unknown-txt str/<print>
	[ #00 ;image/image-type LDA DUP ADD ] ;image-types ADD2 LDA2 str/<print>
	#0a18 DEO
	JMP2r

	&error-unknown-txt "Unsupported 20 "tga-type: 20 $1

@tga/<parse-bw> ( -- )
	#00 ;image/depth LDA #03 SFT .File/length DEO2
	#0000 #0000
	&>stream
		;&pixel .File/read DEO2
		.File/success-lb ] DEI ?{ POP2 POP2 JMP2r }
		OVR2 OVR2 ROTk ROT POP
		( | draw )
		[ LIT2 00 _&pixel ] LDR #16 SFT ;&bw-lut ADD2 LDA2 JSR2 canvas/<set-pixel>
		SWP2 INC2 DUP2 ;image/w LDA2 NEQ2 ?{
			( linebreak ) POP2 #0000 SWP2 INC2 SWP2 }
		SWP2 !&>stream
	&pixel $8
	&icn-ext "00x00.icn 00
	&bw-lut [
	=&shade-a =patternize/a
	=patternize/b =&shade-d ]

@tga/shade-a ( -- pixel )
	POP2 #01 JMP2r

@tga/shade-d ( -- pixel )
	POP2 #00 JMP2r

@image-types-txts
	&null "no-data $1
	&rawc "RAW-color $1
	&rawt "RAW-true $1
	&rawbw "RAW-bw $1
	&rlec "RLE-color $1
	&rlet "RLE-true $1
	&rlebw "RLE-bw $1
	&err "err $1

@image-types [
	=image-types-txts/null =image-types-txts/rawc
	=image-types-txts/rawt =image-types-txts/rawbw
	=image-types-txts/err =image-types-txts/err
	=image-types-txts/err =image-types-txts/err
	=image-types-txts/err =image-types-txts/rlec
	=image-types-txts/rlet =image-types-txts/rlebw ]

@image &id-length $1 &color-map $1 &image-type $1 &map $5 &x $2 &y $2 &w $2 &h $2 &depth $1 &descriptor $1

~noodle-manifest.tal

