( devices )
|00 @System/vector $2 &expansion $2 &wst $1 &rst $1 &metadata $2 &r $2 &g $2 &b $2 &debug $1 &state $1
|10 @Console/vector $2 &read $5 &type $1 &write $1 &error $1
|20 @Screen/vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller/vector $2 &button $1 &key $1
|90 @Mouse/vector $2 &x $2 &y $2 &state $1 &pad $3 &sx $2 &sy $1 &sy-lb $1
|a0 @File/vector $2 &success $1 &success-lb $1 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2

( variables )
|0000
@circle-cx      $2
@circle-cy      $2
@circle-r       $2
@circle-rsq     $2
@r-direction    $1
@timer          $1
@anim-delay     $1
@anim-delay-inhale $1
@current-val $1
@max-breath     $1
@state          $1
@view         $1

( constants )
%CIRCLE-R-MIN  { #0020 }
%CIRCLE-R-MAX  { #0035 }

( --- Tabbar Layout --- )
%TABBAR-HEIGHT { #0010 }
%TABBAR-Y      { #0000 }

( --- Sidebar Anchor Layout --- )
%SIDEBAR-X      { #0008 }
%SIDEBAR-Y      { #0000 }
%BTN-SIZE       { #0008 }

( --- Bitmasks --- )
%MSK-PLAYING  { #01 }
%MSK-OPEN     { #02 }
%MSK-PRESSED  { #04 }

%SCR-TITLE    { #01 }
%SCR-BREATH   { #02 }
%SCR-HOLD     { #03 }
%SCR-INHALE   { #04 }
%SCR-RESULT   { #05 }

( main program )
|0100
    ;meta #06 DEO2

    #012e .Screen/width DEO2
    #00f0 .Screen/height DEO2


    load-theme

    CIRCLE-R-MIN .circle-r STZ2
    #03 .anim-delay STZ
    #04 .anim-delay-inhale STZ
    #00 .current-val STZ
    #1e .max-breath STZ

    MSK-OPEN .state STZ
    SCR-BREATH .view STZ

    ;on-frame .Screen/vector DEO2
    
    ,&skip-this JCN
    inbe-rom-path ;load-rom JSR2
    &skip-this
BRK

@meta $1
	( name ) "Inner "Breeze 0a
	( details ) "A 20 "guided 20 "breathing 20 "meditation 0a
	( author ) "By 20 "Waozi 0a
	( date ) &date "18 20 "Feb 20 "2026 $1
	01
	( icon ) 83 =appicon

@load-theme ( -- )
	;&path .File/name DEO2
	#0002 .File/length DEO2
	;&r .File/read DEO2
	;&g .File/read DEO2
	;&b .File/read DEO2
	.File/success DEI2 ORA #01 [ JCN JMP2r ]
	LIT2 &r $2 .System/r DEO2
	LIT2 &g $2 .System/g DEO2
	LIT2 &b $2 .System/b DEO2

JMP2r
	&path ".theme $1

@breath-logic
    ( --- Normal Breathing Logic --- )
    .timer LDZ .anim-delay LDZ LTH ,&return JCN
    #00 .timer STZ

    .r-direction LDZ #01 EQU ,&shrink JCN

    &grow
        .circle-r LDZ2 INC2 .circle-r STZ2
        .circle-r LDZ2 CIRCLE-R-MAX LTH2 ,&return JCN
        #01 .r-direction STZ
        ,&return JMP

    &shrink
        .circle-r LDZ2 #0001 SUB2 .circle-r STZ2
        .circle-r LDZ2 CIRCLE-R-MIN GTH2 ,&return JCN
        #00 .r-direction STZ

        ( Breath Counter Logic )
        .current-val LDZ INC .current-val STZ
        .current-val LDZ .max-breath LDZ LTH ,&return JCN
        
        SCR-HOLD .view STZ
        #00 .current-val STZ

    &return
    JMP2r
 
( --- Hold Logic: Increment every 60 frames --- )
@hold-logic
    ( Increment once per second )
    .timer LDZ #3c LTH ,&draw JCN
    #00 .timer STZ
    .current-val LDZ INC .current-val STZ

    &draw
    ( Set UI positions )
    .circle-cx LDZ2 #0014 SUB2 .Screen/x DEO2
    .circle-cy LDZ2 #0035 ADD2 .Screen/y DEO2
    ;fill-icn .Screen/addr DEO2

    &loop-bg
    #03 .Screen/sprite DEO
    .Screen/x DEI2 INC2 DUP2 .Screen/x DEO2
    .circle-cx LDZ2 #0015 ADD2 LTH2 ,&loop-bg JCN

    ( Draw Label )
    .circle-cx LDZ2 #0014 SUB2 .Screen/x DEO2
    .circle-cy LDZ2 #0035 ADD2 .Screen/y DEO2
    ;txt-breath ;draw-zstr JSR2

    JMP2r

@inhale-logic
    ( State Machine: 0=Inhale, 1=Hold, 2=Exhale )
    .r-direction LDZ #01 EQU ,&hold JCN
    .r-direction LDZ #02 EQU ,&exhale JCN

    &inhale
        .timer LDZ .anim-delay-inhale LDZ LTH ,&check JCN
        #00 .timer STZ
        .circle-r LDZ2 INC2 .circle-r STZ2
        .circle-r LDZ2 CIRCLE-R-MAX LTH2 ,&return JCN
        ( Hit Max: Switch to Hold State )
        #01 .r-direction STZ
        #00 .current-val STZ ( Reset frame counter )
        #00 .timer STZ       ( Reset seconds counter )
        ,&return JMP

    &exhale
        .timer LDZ .anim-delay-inhale LDZ LTH ,&check JCN
        #00 .timer STZ
        .circle-r LDZ2 #0001 SUB2 .circle-r STZ2
        .circle-r LDZ2 CIRCLE-R-MIN GTH2 ,&return JCN
        ( Hit Min: Reset and go back to Breath screen )
        #00 .r-direction STZ
        #00 .current-val STZ
        SCR-BREATH .view STZ
        ,&return JMP

    &check
    ( If not in hold state, just return )
    .r-direction LDZ #01 NEQ ,&return JCN

    &hold
        ( We use .current-val to count 60 frames )
        .timer LDZ #3c LTH ,&return JCN
        #00 .timer STZ
        .current-val LDZ INC .current-val STZ
        
        ( Check if we reached 15 seconds )
        .current-val LDZ #0f LTH ,&return JCN
        
        ( 15 Seconds Done: Switch to Exhale State )
        #02 .r-direction STZ
        #00 .timer STZ
        
    &return
    JMP2r

@on-frame
    #00 .Screen/auto DEO
    ;handle-mouse JSR2
    ;clear-screen JSR2
    ;draw-ui JSR2


    .Screen/width DEI2 #01 SFT2 #0020 SUB2 .Screen/x DEO2
    #0000 .Screen/y DEO2

    SCR-BREATH .view LDZ NEQ ,&skip-breath-text JCN
    ;txt-breath ;draw-zstr JSR2

    &skip-breath-text

    SCR-HOLD .view LDZ NEQ ,&skip-hold-text JCN
    ;txt-hold ;draw-zstr JSR2
    ;txt-it ;draw-zstr JSR2

    &skip-hold-text

    SCR-INHALE .view LDZ NEQ ,&skip-inhale-text JCN
    ;txt-breath ;draw-zstr JSR2
    ;txt-in ;draw-zstr JSR2

    &skip-inhale-text


    ( Only process timing if MSK-PLAYING is set )
    .state LDZ MSK-PLAYING AND #00 EQU ,draw JCN

    (animate )
    .timer LDZ INC .timer STZ

    ( Check if we are in HOLD view )
    .view LDZ SCR-BREATH NEQ ,&no-breath JCN
        ;breath-logic JSR2
    &no-breath
    .view LDZ SCR-HOLD NEQ ,&no-hold JCN
        ;hold-logic JSR2
    &no-hold
    .view LDZ SCR-INHALE NEQ ,&no-inhale JCN
        ;inhale-logic JSR2
    &no-inhale

    @draw
    .Screen/width DEI2 #01 SFT2 .Screen/height DEI2 #01 SFT2 .circle-r LDZ2
    ;draw-circle JSR2
    ;draw-center-stats JSR2

    .Mouse/x DEI2 .Screen/x DEO2 .Mouse/y DEI2 .Screen/y DEO2
    ;sprite-cursor .Screen/addr DEO2 #01 .Screen/sprite DEO
BRK2

@handle-back-button ( -- )
	.Mouse/y DEI2 #0010 LTH2
    .Mouse/x DEI2 #0100 GTH2 AND ,click-back JCN
    JMP2r
	@click-back
    ;potato-rom-path ;load-rom JSR2

@handle-hold-button ( -- )
    .view LDZ SCR-HOLD NEQ ,&skip JCN

    ( Check Y: circle-cy + 0x35 <= Mouse/y < circle-cy + 0x3d )
    .circle-cy LDZ2 #0035 ADD2
    DUP2 .Mouse/y DEI2 SWP2 LTH2 ,&skip JCN
    #0008 ADD2
    .Mouse/y DEI2 SWP2 GTH2 ,&skip JCN

    ( Check X: circle-cx - 0x14 <= Mouse/x < circle-cx + 0x1d )
    .circle-cx LDZ2 #0014 SUB2
    DUP2 .Mouse/x DEI2 SWP2 LTH2 ,&skip JCN
    #0031 ADD2
    .Mouse/x DEI2 SWP2 GTH2 ,&skip JCN

    ;to-inhale JMP2

    &skip JMP2r

@handle-mouse
    .Mouse/state DEI #00 EQU ;released JCN2
    .state LDZ MSK-PRESSED AND ;done JCN2
    .state LDZ MSK-PRESSED ORA .state STZ

    ;handle-back-button JSR2
    ;handle-hold-button JSR2

    ( Sidebar Toggle Check )
    .Mouse/y DEI2 SIDEBAR-Y LTH2 ,check-controls JCN
    .Mouse/y DEI2 SIDEBAR-Y BTN-SIZE ADD2 GTH2 ,check-controls JCN
    .Mouse/x DEI2 SIDEBAR-X #0048 ADD2 LTH2 ,check-controls JCN
    .Mouse/x DEI2 SIDEBAR-X #0048 ADD2 BTN-SIZE ADD2 GTH2 ,check-controls JCN
        .state LDZ MSK-OPEN EOR .state STZ
        ;done JMP2

    @check-controls
    ;handle-mouse-controls JSR2

    @done JMP2r
    @released
        .state LDZ MSK-PRESSED #ff EOR AND .state STZ
JMP2r

@handle-mouse-controls
    .state LDZ MSK-OPEN AND #00 EQU ,skip-controls JCN
    ;handle-speed-logic JSR2
    ;handle-breath-logic JSR2
    ;handle-play-logic JSR2
    @skip-controls JMP2r


@to-breath
    SCR-BREATH .view STZ
    #00 .current-val STZ
    CIRCLE-R-MIN .circle-r STZ2
    JMP2r

@to-hold
    SCR-HOLD .view STZ
    #00 .current-val STZ
    CIRCLE-R-MIN .circle-r STZ2
    JMP2r

@to-inhale
    .r-direction STZ #01
    SCR-INHALE .view STZ
    #00 .current-val STZ
    CIRCLE-R-MIN .circle-r STZ2
    JMP2r

@handle-play-logic
    #0094 row-hit? #00 EQU ;skip-play JCN2

    ( Seek Back - Offset #0000 )
    #0000 btn-hit? #00 EQU ,&check-play JCN

        ( If view == BREATH, just reset current-val )
        .view LDZ SCR-BREATH EQU ;to-breath JMP2

        ( If view == HOLD, go to BREATH )
        .view LDZ SCR-HOLD EQU ;to-breath JMP2

        ( If view == INHALE, go to HOLD )
        .view LDZ SCR-INHALE EQU ;to-hold JMP2

        ;skip-play JCN2 ( Safety skip )


    &check-play ( Play/Pause - Offset #0010 )
    #0010 btn-hit? #00 EQU ,&check-seek-forward JCN
        .state LDZ MSK-PLAYING EOR .state STZ
        ;skip-play JCN2

    &check-seek-forward ( Seek Forward - Offset #0020 )
    #0020 btn-hit? #00 EQU ;skip-play JCN2

        ( If view == BREATH, go to hold state )
        .view LDZ SCR-BREATH EQU ;to-hold JMP2

        ( If view == HOLD, go to inhale state )
        .view LDZ SCR-HOLD EQU ;to-inhale JMP2


        ;skip-play JCN2

    @skip-play JMP2r

@handle-speed-logic
    #0024 row-hit? #00 EQU ,skip-speed JCN

    #0000 btn-hit? #00 NEQ ,dec-speed JCN
    #0020 btn-hit? #00 EQU ,skip-speed JCN

    .anim-delay LDZ #1e EQU ,skip-speed JCN
    .anim-delay LDZ INC .anim-delay STZ
    ,skip-speed JMP

    @dec-speed
    .anim-delay LDZ #01 EQU ,skip-speed JCN
    .anim-delay LDZ #01 SUB .anim-delay STZ   ( DEC -> #01 SUB )

    @skip-speed JMP2r

@handle-breath-logic
    #004c row-hit? #00 EQU ,skip-breath JCN

    #0000 btn-hit? #00 NEQ ,dec-breath JCN
    #0020 btn-hit? #00 EQU ,skip-breath JCN

    .max-breath LDZ #2b EQU ,skip-breath JCN
    .max-breath LDZ INC .max-breath STZ
    ,skip-breath JMP

    @dec-breath
    .max-breath LDZ #01 EQU ,skip-breath JCN
    .max-breath LDZ #01 SUB .max-breath STZ   ( DEC -> #01 SUB )

    @skip-breath JMP2r

@draw-ui
    ;draw-tabbar JSR2
    ;draw-ui-controls JSR2
JMP2r

@draw-ui-controls
    ( Header Content )
    SIDEBAR-X .Screen/x DEO2 SIDEBAR-Y .Screen/y DEO2
    ;txt-controls ;draw-zstr JSR2
    SIDEBAR-X #0048 ADD2 .Screen/x DEO2 SIDEBAR-Y .Screen/y DEO2
    .state LDZ MSK-OPEN AND ,show-up JCN
        ;sprite-down .Screen/addr DEO2 ,render-toggle JMP
    @show-up ;sprite-up .Screen/addr DEO2
    @render-toggle #01 .Screen/sprite DEO

    ( Draw content if open )

    .state LDZ MSK-OPEN AND #00 EQU ;skip-children JCN2
        ( Speed Section )
        SIDEBAR-X .Screen/x DEO2 SIDEBAR-Y #0018 ADD2 .Screen/y DEO2
        ;txt-speed ;draw-zstr JSR2
        .anim-delay LDZ SIDEBAR-Y #0024 ADD2 ;draw-counter JSR2

        ( Breaths Section )
        SIDEBAR-X .Screen/x DEO2 SIDEBAR-Y #003c ADD2 .Screen/y DEO2
        ;txt-breaths ;draw-zstr JSR2
        .max-breath LDZ SIDEBAR-Y #004c ADD2 ;draw-counter JSR2

        ( Player Control Section )
        SIDEBAR-X .Screen/x DEO2 SIDEBAR-Y #0084 ADD2 .Screen/y DEO2
        ;txt-play ;draw-zstr JSR2
        SIDEBAR-Y #0094 ADD2 .Screen/y DEO2


        SIDEBAR-X .Screen/x DEO2
        ;sprite-seek .Screen/addr DEO2
        #11 .Screen/sprite DEO

        SIDEBAR-X #0010 ADD2 .Screen/x DEO2
        .state LDZ MSK-PLAYING AND ,show-pause JCN
        ;sprite-play .Screen/addr DEO2 ,render-player JMP
        @show-pause ;sprite-pause .Screen/addr DEO2
        @render-player
        #01 .Screen/sprite DEO

        SIDEBAR-X #0020 ADD2 .Screen/x DEO2
        ;sprite-seek .Screen/addr DEO2
        #01 .Screen/sprite DEO
    @skip-children
JMP2r

( --- Tabbar Drawing --- )

@draw-tabbar-bg ( -- )
	;fill-icn .Screen/addr DEO2
	#0000 .Screen/y DEO2
	#0000 .Screen/x DEO2
    @tbx #03 .Screen/sprite DEO
        .Screen/x DEI2 INC2 DUP2 .Screen/x DEO2
        .Screen/width DEI2 LTH2 ,tbx JCN
	JMP2r

@draw-tabbar-text ( -- )
	#0000 .Screen/y DEO2
	#0100 .Screen/x DEO2
	;tab-back ;draw-zstr JSR2
JMP2r

@draw-tabbar ( -- )
	;draw-tabbar-bg JSR2
	;draw-tabbar-text JSR2
	JMP2r

@draw-counter ( val y* -- )
    .Screen/y DEO2            ( Set Y coordinate )
    SIDEBAR-X .Screen/x DEO2  ( Start at sidebar X )

    ( Minus Sprite )
    ;sprite-minus .Screen/addr DEO2 #01 .Screen/sprite DEO

    ( Number )
    SIDEBAR-X #000c ADD2 .Screen/x DEO2
    ;draw-num JSR2            ( val is already on stack )

    ( Plus Sprite )
    SIDEBAR-X #0020 ADD2 .Screen/x DEO2
    ;sprite-plus .Screen/addr DEO2 #01 .Screen/sprite DEO
JMP2r


@draw-num ( val -- )
    DUP #0a DIV             ( Get Tens )
    DUP #30 ADD ;draw-char JSR2 ( Print Tens )
    #0a MUL SUB             ( Original - Tens*10 = Ones )
    #30 ADD ;draw-char JSR2 ( Print Ones )
JMP2r

@draw-char ( char -- )
    #20 SUB            ( Align: Space is now index 0 )
    #00 SWP            ( Byte to Short )
    #30 SFT2           ( Multiply by 8 )
    ;font ADD2 .Screen/addr DEO2
    #41 .Screen/sprite DEO
    .Screen/x DEI2 #0008 ADD2 .Screen/x DEO2
JMP2r

@draw-center-stats
    .Screen/width DEI2 #01 SFT2 #0008 SUB2 .Screen/x DEO2
    .Screen/height DEI2 #01 SFT2 #0004 SUB2 .Screen/y DEO2
    .current-val LDZ

    DUP #0a DIV         ( Get Tens )
    SWP #0a DIVk MUL SUB ( Get Ones )

    SWP #30 ADD ;draw-center-char JSR2
    #30 ADD ;draw-center-char JSR2
JMP2r

@draw-center-char ( char -- )
    #20 SUB            ( Align: Must match draw-char! )
    #00 SWP            ( Byte to Short )
    #30 SFT2           ( Multiply by 8 )
    ;font ADD2 .Screen/addr DEO2
    #41 .Screen/sprite DEO ( Color mode for center stats )
    .Screen/x DEI2 #0008 ADD2 .Screen/x DEO2
JMP2r

@clear-screen
    #00 .Screen/auto DEO #0000 .Screen/y DEO2
    @y #0000 .Screen/x DEO2
        @x #40 .Screen/pixel DEO #00 .Screen/pixel DEO
            .Screen/x DEI2 INC2 DUP2 .Screen/x DEO2
            .Screen/width DEI2 LTH2 ,x JCN
        POP2 .Screen/y DEI2 INC2 DUP2 .Screen/y DEO2
        .Screen/height DEI2 LTH2 ,y JCN
JMP2r

@draw-circle ( x* y* r* -- )
    .circle-r STZ2 .circle-cy STZ2 .circle-cx STZ2
    .circle-r LDZ2 DUP2 MUL2 .circle-rsq STZ2
    .circle-cy LDZ2 .circle-r LDZ2 SUB2
    @y-loop STH2 .circle-cx LDZ2 .circle-r LDZ2 SUB2
        @x-loop DUP2 .circle-cx LDZ2 SUB2 DUP2 MUL2
            STH2r DUP2 STH2 .circle-cy LDZ2 SUB2 DUP2 MUL2 ADD2
            .circle-rsq LDZ2 GTH2 ,skip-pixel JCN
                DUP2 .Screen/x DEO2 STH2r DUP2 STH2 .Screen/y DEO2 #03 .Screen/pixel DEO
            @skip-pixel #0001 ADD2
            DUP2 .circle-cx LDZ2 .circle-r LDZ2 ADD2 LTH2 ,x-loop JCN
        POP2 STH2r #0001 ADD2
        DUP2 .circle-cy LDZ2 .circle-r LDZ2 ADD2 LTH2 ,y-loop JCN
    POP2 JMP2r

@draw-zstr ( addr* -- )
    &loop
        LDAk DUP #00 EQU ,&end JCN ( If char is 0, we are done )
        ;draw-char JSR2             ( Draw the character )
        INC2 ,&loop JMP            ( Move to next character )
    &end
    POP2                           ( Remove the string address from stack )
JMP2r


( Check if mouse Y is within a row: y-offset -- flag )
@row-hit?
    SIDEBAR-Y ADD2
    DUP2 .Mouse/y DEI2 SWP2 LTH2 ,&no JCN
    BTN-SIZE ADD2
    .Mouse/y DEI2 SWP2 GTH2 ,&no JCN
    #01 JMP2r
    &no #00 JMP2r

( Check if mouse X is within a button: x-offset -- flag )
@btn-hit?
    SIDEBAR-X ADD2
    DUP2 .Mouse/x DEI2 SWP2 LTH2 ,&no JCN
    BTN-SIZE ADD2
    .Mouse/x DEI2 SWP2 GTH2 ,&no JCN
    #01 JMP2r
    &no #00 JMP2r


@load-rom ( filename* -- )
	.File/name DEO2
	( clear wst/rst )
	#0000 .System/wst DEO2
	( clear screen )
	#0000 DUP2 .Screen/x DEO2 .Screen/y DEO2
	[ LIT2 80 -Screen/pixel ] DEO
	[ LIT2 c0 -Screen/pixel ] DEO
	( reset device vectors )
	[ LIT2r 0000 ]
	#00
	&device-vector-loop
		DUP2r STHk DEO2r
		#10 ADD
		DUP ?&device-vector-loop
	POP POP2r
	( copy the zero-page-loader into f0-ff )
	;&zero-page-loader LITr f0
	&copy-loop
	LDAk STHkr STZ
	INC2 INCr
	STHkr ,&copy-loop JCN
	POP2 ( leave 00 on return stack )

	( prepare the stack for the zero-page-loader )
	( the more we prepare here in advance, the less we'll have to overwrite )
	STHkr #00fe ( arguments for STZ2 at ff )
	STHkr ( argument for JMP at fe  )
	DUPk #fcfe ( arguments for STZ2 at fd and JMP  )
	OVR2 #fafe ( arguments for STZ2 at fd and JMP  )
	OVR2 #f8fe ( arguments for STZ2 at fd and JMP  )
	OVR2 #f6fe ( arguments for STZ2 at fd and JMP  )
	OVR2 #f401 ( arguments for STZ2 at fd, plus an extra 01 )
	STHkr ( first argument for ADD2 )
	.File/success ( argument for DEI2 )
	#0100 .File/read ( arguments for DEO2 )
	#ff00 .File/length DEO2
	#00f0 JMP2

	&zero-page-loader
	( f0 ) DEO2
	( f1 ) DEI2
	( f2 ) ADD2
	( f3 ) &loop DUPr
	( f4 ) STH2k
	( f5 ) STAr
	( f6 ) INC2
	( f7 ) NEQ2k
	( f8 ) ,&loop
	( f9 )
	( fa ) JCN
	( fb ) POPr
	( fc ) POP2
	( fd ) STZ2 ( deletes f4-fd through looping )
	( fe ) JMP
	( ff ) STZ2 ( deletes fe-ff )

	&tmp $1


( Graphics Data - Sprites )
@sprite-play   [ 0008 0c0e 0f0e 0c08 ]
@sprite-pause  [ 0066 6666 6666 6600 ]
@sprite-plus   [ 1818 187e 7e18 1818 ]
@sprite-minus  [ 0000 007e 7e00 0000 ]
@sprite-cursor [ 80c0 e0f0 f8e0 1010 ]

@fill-icn [
	ffff ffff ffff ffff ]

( Graphics Data - Fonts )
@font
    [ 0000 0000 0000 0000 ] ( 0x20 Space )
    [ 0000 0000 0000 0000 0000 0000 0000 0000 ] ( 0x21-0x22 )
    [ 0000 0000 0000 0000 0000 0000 0000 0000 ] ( 0x23-0x24 )
    [ 0000 0000 0000 0000 0000 0000 0000 0000 ] ( 0x25-0x26 )
    [ 0000 0000 0000 0000 0000 0000 0000 0000 ] ( 0x27-0x28 )
    [ 0000 0000 0000 0000 0000 0000 0000 0000 ] ( 0x29-0x2a )
    [ 1818 187e 7e18 1818 ] ( 0x2b + )
    [ 0000 0000 0000 0000 ] ( 0x2c , )
    [ 0000 007e 7e00 0000 ] ( 0x2d - )
    [ 0000 0000 0000 0000 ] ( 0x2e . )
    [ 0000 0000 0000 0000 ] ( 0x2f / )
    [ 3c46 4e56 6642 3c00 1828 0808 0808 3e00 ( 0x30: 0 1 )
      3c42 023c 4040 7e00 3c42 021c 0242 3c00 ( 2 3 )
      0818 2848 7e08 0800 7e40 7c02 0242 3c00 ( 4 5 )
      3c40 7c42 4242 3c00 7e02 0408 1010 1000 ( 6 7 )
      3c42 423c 4242 3c00 3c42 423e 0202 3c00 ] ( 8 9 )
    [ 0000 0000 0000 0000 0000 0000 0000 0000 ( 0x3a-0x3b )
      0000 0000 0000 0000 0000 0000 0000 0000 ( 0x3c-0x3d )
      0000 0000 0000 0000 0000 0000 0000 0000 ( 0x3e-0x3f )
      0000 0000 0000 0000 ] ( 0x40 @ )
    [ 183c 6666 7e66 6600 7c66 667c 6666 7c00 ( 0x41: A B )
      3c66 6060 6066 3c00 786c 6666 666c 7800 ( C D )
      7e60 607c 6060 7e00 7e60 607c 6060 6000 ( E F )
      3c66 606e 6666 3c00 6666 667e 6666 6600 ( G H )
      3c18 1818 1818 3c00 1e0c 0c0c 0c6c 3800 ( I J )
      666c 7870 786c 6600 6060 6060 6060 7e00 ( K L )
      6377 7f6b 6363 6300 6676 7e7e 6e66 6600 ( M N )
      3c66 6666 6666 3c00 7c66 667c 6060 6000 ( O P )
      3c66 6666 663c 0e00 7c66 667c 786c 6600 ( Q R )
      3c66 603c 0666 3c00 7e18 1818 1818 1800 ( S T )
      6666 6666 6666 3c00 6666 6666 663c 1800 ( U V )
      6363 636b 7f77 6300 6666 3c18 3c66 6600 ( W X )
      6666 663c 1818 1800 7e06 0c18 3060 7e00 ] ( Y Z )

( strings )
@txt-controls "CONTROLS $1
@txt-speed    "SPEED $1
@txt-breaths  "BREATHS $1
@txt-rounds   "ROUNDS $1
@txt-play     "PLAY $1
@txt-breath   "BREATH $1
@txt-in       "IN $1
@txt-out      "OUT $1
@txt-it       "IT $1
@txt-hold     "HOLD $1
( Tab names )
@tab-back     "LOTUS $1

@potato-rom-path "bin/potato.rom $1
@inbe-rom-path "bin/inbe.rom $1

@sprite-up     [ 183c 7e7e 3c3c 1818 ]
@sprite-down   [ 1818 3c3c 7e7e 3c18 ]
@sprite-seek   [ b0b8 bcbe bcb8 b000 ]


@appicon
	0000 0000 0000 0000 0000 0000 0000 0003 0007 1f3f 7f7e 7e7d 071f 3f7f ffff ffff
	0000 0000 1e7e fefe 8080 809f ffff ffff 0307 0f0f 0f0f 0f0f 0408 1010 1010 1010
	7bbf ffc7 e0e0 f0ff ff7f 3f3f 1710 0f0f fcfc f8e0 0000 0000 fefe fcf8 e000 0080
	0f0f 0707 0301 0000 1010 080f 0703 0100 ffff ffff ffff 7f00 1f1f 7fff ffff ff7f
	80c0 c0c0 c080 0000 c0e0 e0e0 e0c0 8000