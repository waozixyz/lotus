( a potato. )

|00 @System &vector $2 &pad $6 &r $2 &g $2 &b $2
|10 @Console &vector $2 &read $1 &pad $5 &write $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller &vector $2 &button $1 &key $1
|90 @Mouse &vector $2 &x $2 &y $2 &state $1 &chord $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|c0 @DateTime &year $2 &month $1 &day $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1

%menu-def { #0a }
%menu-sel { #0b }
%menu-hov { #03 }
%menu-hit { #000c }
%menu-auto { #01 }
%menu-label { DEO }
%menu-l { #30 }
%menu-r { #03 }

%MOD  { DIVk MUL SUB }
%MOD2 { DIV2k MUL2 SUB2 }

|0000

@cursor
	&x $2 &y $2
@active $1
@length $1
@windows ( x* y* size* app* name* )

|0100 ( -> )

	( theme )
	#500f .System/r DEO2
	#b70f .System/g DEO2
	#a70f .System/b DEO2

	;window-name ;app-view #1812 #0023 #0073 ;add-window JSR2
	;window-name2 ;app-pict #2219 #0064 #0032 ;add-window JSR2
	;window-name3 ;app-term #1909 #0080 #0090 ;add-window JSR2

	;draw-menu-bg JSR2
	;draw-menu JSR2
	;draw-clock JSR2
	;draw-desktop JSR2
	;untrap JSR2

	#01 ;sel-window JSR2

BRK

@manifest

	01 "Potato $1
		01 'n :file-new "New $1
	04 "Edit $1
		01 'c :edit-copy "Copy $1
		01 'v :edit-paste "Paste $1
		01 'x :edit-cut "Cut $1
		00 08 :edit-erase "Erase $1
	01 "View $1
		02 00 :toggle-zoom "Zoom $1
	$1

@untrap ( -- )

	;on-mouse .Mouse/vector DEO2

	( ;on-frame .Screen/vector DEO2
	;on-button .Controller/vector DEO2
	 )
	( release mouse ) #00 .Mouse/state DEO

JMP2r

@trap ( -- )

	( ;on-frame-trap .Screen/vector DEO2
	;on-button-trap .Controller/vector DEO2
	;on-mouse-trap .Mouse/vector DEO2 )
	( clear cursor )
	#40 ;draw-cursor JSR2
	( release mouse ) #00 .Mouse/state DEO

JMP2r

@on-mouse ( -> )

	.Mouse/y DEI2 #000c LTH2 ;trap-menu JCN2

	#00 .Screen/auto DEO
	( clear last cursor )
	#40 ;draw-cursor JSR2
	( record mouse positions )
	.Mouse/x DEI2 DUP2 .cursor/x STZ2 .Screen/x DEO2
	.Mouse/y DEI2 DUP2 .cursor/y STZ2 .Screen/y DEO2
	( draw new cursor )
	;cursor-icn .Screen/addr DEO2
	#42 .Mouse/state DEI #00 NEQ ADD .Screen/sprite DEO

	.Mouse/state DEI #00 EQU ,&no-touch JCN
		.Mouse/x DEI2 .Mouse/y DEI2 ;pick-window JSR2
		#00 .Mouse/state DEO
		&no-touch

BRK

@file-new ( -- ) JMP2r
@edit-copy ( -- ) JMP2r
@edit-paste ( -- ) JMP2r
@edit-cut ( -- ) JMP2r
@edit-erase ( -- ) JMP2r
@toggle-zoom ( -- ) JMP2r

@sbyte ( str* -- byte )

	LDAk ,chex JSR STH
	INC2 LDA ,chex JSR
	STHr #40 SFT ADD

JMP2r

@chex ( char -- hex )

	DUP #2f GTH OVR #3a LTH AND ,&number JCN
	DUP #60 GTH OVR #67 LTH AND ,&lc JCN
	DUP #40 GTH OVR #47 LTH AND ,&uc JCN
		POP #00 JMP2r
	&number #30 SUB JMP2r
	&uc #37 SUB JMP2r
	&lc #57 SUB

JMP2r

@add-window ( name* app* h w y* x* -- )

	.length LDZ #0a MUL .windows ADD STH
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHr STZ2
	.length LDZk INC SWP STZ

JMP2r

@pick-window ( x* y* -- id )

	,&y STR2 ,&x STR2
	#00 .length LDZ
	&loop
        DUP #01 SUB #0a MUL .windows ADD #00 SWP [ LIT2 &x $2 ] [ LIT2 &y $2 ] ROT2 ;within-win JSR2 ;print/byte JSR2 #0a18 DEO
        #01 SUB LTHk ,&loop JCN
    POP2

JMP2r

@within-win ( x* y* win* -- flag )

	STH2
	( x0 ) OVR2 STH2kr LDA2 LTH2 ,&fail JCN
	( y0 ) DUP2 STH2kr INC2 INC2 LDA2 LTH2 ,&fail JCN
	( x1 ) OVR2 STH2kr LDA2k SWP2 #0004 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTH2 ,&fail JCN
	( y1 ) DUP2 STH2kr INC2 INC2 LDA2k SWP2 #0003 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTH2 ,&fail JCN
	POP2 POP2 POP2r #01

JMP2r
	&fail POP2 POP2 POP2r #00 JMP2r

@sel-window ( id -- )

	( get app ) #0a MUL .windows ADD #06 ADD #00 SWP LDA2
	( get app/name ) LDA2 ;print-str JSR2 #0a18 DEO

JMP2r

@window-name "FloppyA $1
@window-name2 "akane20x10.icn $1
@window-name3 "session0000 $1

~src/draw.tal
~src/assets.tal
~src/app-view.tal
~src/app-term.tal
~src/app-pict.tal
~src/manifest.tal
~src/font.tal

@buffer
