( a potato. )

|00 @System &vector $2 &pad $6 &r $2 &g $2 &b $2
|10 @Console &vector $2 &read $1 &pad $5 &write $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|80 @Controller &vector $2 &button $1 &key $1
|90 @Mouse &vector $2 &x $2 &y $2 &state $1 &chord $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|c0 @DateTime &year $2 &month $1 &day $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1

%menu-def { #0a }
%menu-sel { #0b }
%menu-hov { #03 }
%menu-hit { #000c }
%menu-auto { #01 }
%menu-label { DEO }
%menu-l { #30 }
%menu-r { #03 }

%MOD  { DIVk MUL SUB }
%MOD2 { DIV2k MUL2 SUB2 }
%LTS2 { #8000 STH2k ADD2 SWP2 STH2r ADD2 GTH2 }
%GTS2 { #8000 STH2k ADD2 SWP2 STH2r ADD2 LTH2 }
%POP? { JMP SWP POP }
%POP2? { JMP SWP2 POP2 }

( debugger )

%BUG { ;print/byte JSR2 #0a18 DEO }
%BUG2 { ;print JSR2 #0a18 DEO }
%BUGk { DUP ;print/byte JSR2 #0a18 DEO }
%BUG2k { DUP2 ;print JSR2 #0a18 DEO }
%ECHO { ;print-str JSR2 #0a18 DEO }

|0000

@size-system-start

@cursor &x $2 &y $2
@anchor &x $2 &y $2

@drag $1
@length $1
@windows
	( x* y* size* app* name* )

|0100 ( -> )

	( theme )
	#500f .System/r DEO2
	#b70f .System/g DEO2
	#a70f .System/b DEO2

	( size )
	#0200 .Screen/width DEO2
	#0200 .Screen/height DEO2

	#ff .drag STZ

	;draw-menu-bg JSR2
	;draw-menu JSR2
	;draw-desktop JSR2
	;untrap JSR2

	;open-look JSR2

BRK

@exit ( -- )

	#010f DEO

JMP2r

@untrap ( -- )

	;on-mouse .Mouse/vector DEO2
	;on-button .Controller/vector DEO2
	;on-frame .Screen/vector DEO2
	( release mouse ) #00 .Mouse/state DEO

JMP2r

@trap ( -- )

	( ;on-frame-trap .Screen/vector DEO2
	;on-button-trap .Controller/vector DEO2
	;on-mouse-trap .Mouse/vector DEO2 )
	( clear cursor )
	#40 ;draw-cursor JSR2
	( release mouse ) #00 .Mouse/state DEO

JMP2r

@on-frame ( -> )

	;update-clock JSR2

BRK

@on-button ( -> )

	.Controller/button DEI2 ;find-modkey JSR2
	ORAk #00 EQU ,&skip JCN
		DUP2 JSR2
		&skip
	POP2

BRK

@on-mouse ( -> )

	.Mouse/y DEI2 #000c LTH2 ;trap-menu JCN2

	#00 .Screen/auto DEO
	( clear last cursor )
	#40 ;draw-cursor JSR2
	( record mouse positions )
	.Mouse/x DEI2 DUP2 .cursor/x STZ2 .Screen/x DEO2
	.Mouse/y DEI2 DUP2 .cursor/y STZ2 .Screen/y DEO2
	( draw new cursor )
	;cursor-icn .Screen/addr DEO2
	#42 .Mouse/state DEI #00 NEQ SUB .Screen/sprite DEO

	.drag LDZ #ff NEQ ;on-drag JCN2
	.Mouse/state DEI .Controller/button DEI #0102 EQU2 ;on-drag-start JCN2

	( pick window )
	.Mouse/state DEI #00 EQU ,&no-touch JCN
		.Mouse/x DEI2 .Mouse/y DEI2 ;pick-win JSR2
		DUP ;sel-win JSR2
		( TODO: cleanup )
		;get-win JSR2
		INC2 INC2 LDA2 .Mouse/y DEI2 SWP2 SUB2
		#000a GTH2 ,&no-bar JCN
			;set-drag JSR2
			&no-bar
		&no-touch

	( send mouse to active win )
	.Mouse/x DEI2 .Mouse/y DEI2 ;get-active-win JSR2
		DUP2 #ffff EQU2 ,&skip JCN
		ROT2k ROT2 ROT2 ;within-win JSR2 ,&send JCN
	&skip
	POP2 POP2 POP2

BRK
	&send ;get-active-app JSR2 #0004 ADD2 LDA2 JMP2

( drag )

@on-drag ( -> )

	.Mouse/state DEI #00 EQU ;on-drag-end JCN2

	.drag LDZ ;get-win JSR2 STH2k

	( clear )
	DUP2 #40 ;draw-drag JSR2

	( move window )
	#000d .Mouse/x DEI2 [ LIT2 &x $2 ] SUB2 STH2kr LDA2 ADD2
		;clamp-win JSR2 STH2kr STA2
	INC2r INC2r
	#001d .Mouse/y DEI2 [ LIT2 &y $2 ] SUB2 STH2kr LDA2 ADD2
		;clamp-win JSR2
		.Screen/height DEI2 #000a SUB2 LTH2k POP2?
		STH2r STA2

	( update )
	.Mouse/x DEI2 .Mouse/y DEI2
		;on-drag/y STA2 ;on-drag/x STA2

	( draw )
	#cf ;draw-drag JSR2

BRK

@clamp-win ( x* -- x* )

	DUP2 ROT2 STH2k SUB2 #8000 GTH2 ,&gth JCN
	POP2r

JMP2r
	&gth POP2 STH2r JMP2r

@on-drag-end ( -> )

	( clear )
	.drag LDZ ;get-win JSR2 #40 ;draw-drag JSR2
	#ff .drag STZ
	;draw-desktop JSR2

BRK

@on-drag-start ( -> )

	.Mouse/x DEI2 .Mouse/y DEI2 ;pick-win JSR2 DUP #ff EQU ,&no-win JCN
		;sel-win JSR2 ;set-drag JSR2

BRK
	&no-win BRK

@set-drag ( -- )

	.length LDZ #01 SUB .drag STZ
	.Mouse/x DEI2 ;on-drag/x STA2
	.Mouse/y DEI2 ;on-drag/y STA2

JMP2r

( window management )

@pick-win ( x* y* -- id )

	,&y STR2 ,&x STR2
	#00 .length LDZ
	&loop
        DUP #01 SUB #0a MUL .windows ADD #00 SWP [ LIT2 &x $2 ] [ LIT2 &y $2 ] ROT2 ;within-win JSR2 ,&found JCN
        #01 SUB LTHk ,&loop JCN
    POP2
    #ff

JMP2r
	&found NIP #01 SUB JMP2r

@within-win ( x* y* win* -- flag )

	STH2
	( x0 ) OVR2 STH2kr LDA2 LTS2 ,&fail JCN
	( y0 ) DUP2 STH2kr INC2 INC2 LDA2 LTS2 ,&fail JCN
	( x1 ) OVR2 STH2kr LDA2k SWP2 #0004 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTS2 ,&fail JCN
	( y1 ) DUP2 STH2kr INC2 INC2 LDA2k SWP2 #0003 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTS2 ,&fail JCN
	POP2 POP2 POP2r #01

JMP2r
	&fail POP2 POP2 POP2r #00 JMP2r

@add-win ( name* app* h w y* x* -- )

	.length LDZ #0a MUL .windows ADD STHk
	STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHr STZ2
	.length LDZk INC SWP STZ
	.length LDZ #01 SUB 

@sel-win ( id -- )

	DUP #ff EQU ;sel-desktop JCN2
	DUP .length LDZ GTH ,&unchanged JCN

	;get-win JSR2 #000a SWP2 ;get-active-win JSR2 ;mswp JSR2

	;draw-desktop JSR2

	( draw menu )
	;get-active-win JSR2 #0006 ADD2 LDA2
	( get manifest )
	LDA2 ;draw-menu/manifest STA2
	;draw-menu-bg JSR2
	;draw-menu JSR2

JMP2r
	&unchanged POP JMP2r

@go-active ( -- )

	.length LDZ
	DUP ,&valid JCN
	POP

JMP2r
	&valid #01 SUB ,sel-win JSR JMP2r

@go-home ( -- )

	;buf/loc #0040 ;mclr JSR2
	LIT '. ;buf/loc STA
	#00 ;sel JSR2

JMP2r

@go-file ( -- )

	;draw-item-text/sel LDA ;get-file JSR2 ;open-file JSR2

JMP2r

@sel-desktop ( ff -- )

	;manifest ;draw-menu/manifest STA2
	;draw-menu-bg JSR2
	;draw-menu JSR2

JMP2r

@close-win ( -- )

	.length LDZk #01 SUB SWP STZ
	;sel-desktop JSR2
	;draw-desktop JSR2

JMP2r

@tab-win ( -- )

	.Screen/height DEI2 #000a SUB2 ;get-active-win JSR2 INC2 INC2 STA2
	;sel-desktop JSR2
	;draw-desktop JSR2

JMP2r

@expand-win ( -- )

	#001d ;get-active-win JSR2 INC2 INC2 STA2
	;draw-desktop JSR2

JMP2r

@center-win ( -- )

	;get-active-win JSR2
	STH2k
	( /w ) #0004 ADD2
	( -x ) LDAk #00 SWP #31 SFT2 .Screen/width DEI2 #01 SFT2 SWP2 SUB2 STH2kr STA2
	( /h ) INC2 INC2r INC2r
	( -y ) LDA #00 SWP #31 SFT2 .Screen/height DEI2 #01 SFT2 SWP2 SUB2 STH2r STA2
	;draw-desktop JSR2

JMP2r

@get-active-app ( -- addr* )

	;get-active-win JSR2 #0006 ADD2 LDA2

JMP2r

@get-active-win ( -- addr* )

	.length LDZ #01 SUB

@get-win ( id -- addr* )

	DUP #ff NEQ ,&exists JCN
		POP #ffff JMP2r
		&exists
	#0a MUL .windows ADD #00 SWP

JMP2r

@open-file ( file* -- )

	LDAk LIT '- EQU ,open-folder JCN
	LDAk LIT '! EQU ;open-potato JCN2
	DUP2 ;scap JSR2 #0004 SUB2 ;dict/icn-ext ;scmp JSR2 ,open-pict JCN

	;get-path JSR2 ;open-text JSR2

JMP2r

@open-potato ( file* -- )

	;buf/loc #0040 ;mclr JSR2
	LIT '. ;buf/loc STA
	#00 ;sel JSR2

JMP2r

@open-folder ( file* -- )

	#0005 ADD2 ;buf/loc ;scpy JSR2
	#00 ;sel JSR2

JMP2r

@open-text ( file* -- )

	;app-text #3023 #001d #0034 ;add-win JSR2

JMP2r

@open-pict ( file* -- )

	;get-path JSR2
	DUP2
	( read size from path )
	;read-pict-size JSR2
	ORAk ,&has-size JCN
		( default size )
		POP2 #2010
		&has-size
	INC INC STH2
	;app-pict STH2r #0020 DUP2 ;add-win JSR2

JMP2r

@get-path ( file* -- abs* )

	;buf/tmp #0040 ;mclr JSR2
	;buf/loc ;buf/tmp ;scat JSR2
	LIT '/ ;buf/tmp ;sput JSR2
	#0005 ADD2 ;buf/tmp ;scat JSR2
	;buf/tmp

JMP2r

@read-pict-size ( filename* -- size* )

	;scap JSR2 #0009 SUB2 ;read-size JSR2

JMP2r

( helpers )

@mswp ( len* a* b* -- )

	,&b STR2 ,&a STR2
	#0000
	&l
		DUP2 [ LIT2 &a $2 ] ADD2 LDAk STH
		OVR2 [ LIT2 &b $2 ] ADD2 LDAk STH
		SWPr
		STHr ROT ROT STA
		STHr ROT ROT STA
		INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@sbyte ( str* -- byte )

	LDAk ,chex JSR STH
	INC2 LDA ,chex JSR
	STHr #40 SFT ADD

JMP2r

@chex ( char -- hex )

	DUP #2f GTH OVR #3a LTH AND ,&number JCN
	DUP #60 GTH OVR #67 LTH AND ,&lc JCN
	DUP #40 GTH OVR #47 LTH AND ,&uc JCN
		POP #00 JMP2r
	&number #30 SUB JMP2r
	&uc #37 SUB JMP2r
	&lc #57 SUB

JMP2r

@shex ( str* -- val* )

	LIT2r 0000
	&w
		LITr 40 SFT2r
		LITr 00
		LDAk ,chex JSR STH ADD2r
		INC2 LDAk ,&w JCN
	POP2
	STH2r

JMP2r

@get-chr ( addr* pixel -- color )

	STH DUP2 STHkr ,get-icn JSR
	ROT ROT #0008 ADD2 STHr ,get-icn JSR DUP ADD ORA

JMP2r

@get-icn ( addr* pixel -- color )

	( y ) STHk #00 SWP #03 SFT2 ADD2 LDA
	( x ) STHr #07 AND #07 SWP SUB SFT #01 AND

JMP2r

@remove-lb ( addr* -- )

	&w
		LDAk #0a NEQ ,&no-lb JCN
			STH2k #00 STH2r STA
			&no-lb
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@rel-mouse ( x* y* win* -- x* y* win* )

	STH2k
	( y ) INC2 INC2 LDA2 SUB2 #0012 SUB2
	SWP2
	( x ) STH2kr LDA2 SUB2 #0008 SUB2
	SWP2
	STH2r

JMP2r

@scmp ( a* b* -- f ) STH2 &l LDAk LDAkr STHr ANDk #00 EQU ,&e JCN NEQk ,&e JCN POP2 INC2 INC2r ,&l JMP &e NIP2 POP2r EQU JMP2r

@print-str ( str* -- )

	&w
		LDAk #18 DEO
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@no-name $1

~src/draw.tal
~src/manifest.tal
@size-system-end

@size-desktop-start
	~src/desktop.tal
@size-desktop-end

@size-apps-start
	~src/apps.tal
@size-apps-end

@size-assets-start
	~src/assets.tal
@size-assets-end

@buf
	&loc '. $3f
	&tmp $40
	&dir $400
	&app
