( a potato. )

|00 @System &vector $2 &pad $6 &r $2 &g $2 &b $2
|10 @Console &vector $2 &read $1 &pad $5 &write $1
|20 @Screen &vector $2 &width $2 &height $2 &auto $1 &pad $1 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1
|30 @Audio0 &vector $2 &position $2 &output $1 &pad $3 &adsr $2 &length $2 &addr $2 &volume $1 &pitch $1
|80 @Controller &vector $2 &button $1 &key $1
|90 @Mouse &vector $2 &x $2 &y $2 &state $1 &chord $1
|a0 @File &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|b0 @Disk &vector $2 &success $2 &stat $2 &delete $1 &append $1 &name $2 &length $2 &read $2 &write $2
|c0 @DateTime &year $2 &month $1 &day $1 &hour $1 &minute $1 &second $1 &dotw $1 &doty $2 &isdst $1

%menu-def { #0a }
%menu-sel { #0b }
%menu-hov { #03 }
%menu-hit { #000c }
%menu-auto { #01 }
%menu-label { DEO }
%menu-l { #30 }
%menu-r { #03 }

%MOD { DIVk MUL SUB }
%MOD2 { DIV2k MUL2 SUB2 }
%LTS2 { #8000 STH2k ADD2 SWP2 STH2r ADD2 GTH2 }
%GTS2 { #8000 STH2k ADD2 SWP2 STH2r ADD2 LTH2 }
%POP? { JMP SWP POP }
%POP2? { JMP SWP2 POP2 }

( debugger )

%BUG { ;print/byte JSR2 #0a18 DEO }
%BUG2 { ;print JSR2 #0a18 DEO }
%BUGk { DUP ;print/byte JSR2 #0a18 DEO }
%BUG2k { DUP2 ;print JSR2 #0a18 DEO }
%ECHO { ;pstr JSR2 #0a18 DEO }

|0000

@size-system-start

@cursor
	&x $2 &y $2 &color $1
@drag $1
@length $1

|0100 ( -> )

	( theme )
	#500f .System/r DEO2
	#b70f .System/g DEO2
	#a70f .System/b DEO2
	;dict/theme-ext ;load-theme JSR2

	( 800x520 | 64:41 )
	#0320 .Screen/width DEO2
	#0208 .Screen/height DEO2

	#ff .drag STZ

	;draw-menu-bg JSR2
	;draw-menu JSR2
	;dict/home-ext ;set-dir JSR2
	;untrap JSR2

BRK

@exit ( -- )

	#010f DEO

JMP2r

@untrap ( -- )

	;on-mouse .Mouse/vector DEO2
	;on-button .Controller/vector DEO2
	;on-frame .Screen/vector DEO2
	( release mouse ) #00 .Mouse/state DEO

JMP2r

@on-frame ( -> )

	;update-clock JSR2

BRK

@on-button ( -> )

	.Controller/button DEI2 ;find-modkey JSR2
	ORAk #00 EQU ,&skip JCN
		( blocking ) JSR2 BRK
		&skip
	POP2

	( send button to active win )
	;get-active-win JSR2
	DUP2 #ffff EQU2 ,&no-win JCN
		.Controller/button DEI2
		SWP2 DUP2
		( app ) #0006 ADD2 LDA2
		( app/button ) #0006 ADD2 LDA2 JMP2
	&no-win
	POP2

BRK

@on-mouse ( -> )

	.Mouse/y DEI2 #000c LTH2 ;trap-menu JCN2

	#42 .Mouse/state DEI #00 NEQ SUB ;cursor-icn ;update-cursor JSR2

	( handle drag )
	.drag LDZ #ff NEQ ;on-drag JCN2
	.Mouse/state DEI .Controller/button DEI #0102 EQU2 ;on-drag-start JCN2

	( pick window )
	.Mouse/state DEI #00 EQU ,&no-touch-win JCN
		.Mouse/x DEI2 .Mouse/y DEI2 ;pick-win JSR2
		DUP #ff EQU ,touch-desktop JCN
		DUP ;sel-win JSR2
		( pick dragbar )
		;get-win JSR2
		INC2 INC2 LDA2 .Mouse/y DEI2 SWP2 SUB2
		#000a GTH2 ,&no-bar JCN
			;set-drag JSR2
			&no-bar
		&no-touch-win

	( send mouse to active win )
	.Mouse/x DEI2 .Mouse/y DEI2 ;get-active-win JSR2
		DUP2 #ffff EQU2 ,&skip JCN
		ROT2k ROT2 ROT2 ;within-win JSR2 ,&send JCN
	&skip

	POP2 POP2 POP2

BRK
	&send
		( app ) ;get-active-win JSR2 #0006 ADD2 LDA2
		( app/mouse ) #0004 ADD2 LDA2 JMP2

@touch-desktop ( -> )

	.Mouse/x DEI2 .Mouse/y DEI2 ;pick-icon JSR2
	.Mouse/state DEI #01 GTH ,&launch JCN
	;sel-icon JSR2
	;sel-desktop JSR2
	#00 .Mouse/state DEO
	POP

BRK
	&launch
		;get-file JSR2 ;open-file JSR2
		#00 .Mouse/state DEO
		POP
	BRK

( drag )

@on-drag ( -> )

	.Mouse/state DEI #00 EQU ;on-drag-end JCN2

	.drag LDZ ;get-win JSR2 STH2k

	( clear )
	DUP2 #40 ;draw-drag JSR2

	( move window )
	#0008 .Mouse/x DEI2 [ LIT2 &x $2 ] SUB2 STH2kr LDA2 ADD2
		;clamp-win JSR2 STH2kr STA2
	INC2r INC2r
	#0018 .Mouse/y DEI2 [ LIT2 &y $2 ] SUB2 STH2kr LDA2 ADD2
		;clamp-win JSR2
		.Screen/height DEI2 #000a SUB2 LTH2k POP2?
		STH2r STA2

	( update )
	.Mouse/x DEI2 .Mouse/y DEI2
		;on-drag/y STA2 ;on-drag/x STA2

	( draw )
	#cf ;draw-drag JSR2

BRK

@clamp-win ( x* -- x* )

	DUP2 ROT2 STH2k SUB2 #8000 GTH2 ,&gth JCN
	POP2r

JMP2r
	&gth POP2 STH2r JMP2r

@on-drag-end ( -> )

	( clear )
	.drag LDZ ;get-win JSR2 #40 ;draw-drag JSR2
	#ff .drag STZ
	;draw-desktop JSR2

BRK

@on-drag-start ( -> )

	.Mouse/x DEI2 .Mouse/y DEI2 ;pick-win JSR2 DUP #ff EQU ,&no-win JCN
		;sel-win JSR2 ;set-drag JSR2

BRK
	&no-win BRK

@set-drag ( -- )

	.length LDZ #01 SUB .drag STZ
	.Mouse/x DEI2 ;on-drag/x STA2
	.Mouse/y DEI2 ;on-drag/y STA2

JMP2r

( directory operations )

@set-dir ( path* -- )

	;validate-dir JSR2

	;buf/loc ;scpy JSR2
	#00 ;sel-icon JSR2

JMP2r

@push-dir ( path* -- )

	;validate-dir JSR2
	DUP2 ;dict/parent-ext ;scmp JSR2 #01 NEQ ,&no-parent JCN
		POP2 ,pop-dir JMP
		&no-parent
	LIT '/ ;buf/loc ;sput JSR2
	;buf/loc ;scat JSR2
	#00 ;sel-icon JSR2

JMP2r

@pop-dir ( -- )

	;validate-dir JSR2
	;buf/loc ;dict/home-ext ;scmp JSR2 ,&skip JCN
	;buf/loc ;buf/loc ;scap JSR2
	&loop
		LDAk LIT '/ EQU ,&found JCN
		#0001 SUB2 LTH2k ,&loop JCN
		&found
	NIP2
	#00 ROT ROT STA
	&skip
	#00 ;sel-icon JSR2

JMP2r

@validate-dir ( -- )

	.length LDZ #00 EQUk ,&skip JCN
	&loop
		DUP ;get-win JSR2
		( win/name ) #0008 ADD2 LDA2 ;no-name NEQ2 ,&busy JCN
		INC GTHk ,&loop JCN
	&skip
	POP2

JMP2r
	&busy POP2r NIP ;sel-win JSR2 ;center-win JSR2 POP2 JMP2r

( open a file )

@open-file ( file* -- )

	DUP2 #ffff EQU2 ,&no-file JCN
	LDAk LIT '- EQU ,open-folder JCN
	DUP2 ;dict/icn-ext ;has-ext JSR2 ;open-pict JCN2
	DUP2 ;dict/chr-ext ;has-ext JSR2 ;open-pict JCN2
	DUP2 ;dict/uf2-ext ;has-ext JSR2 ;open-font JCN2
	DUP2 ;dict/pcm-ext ;has-ext JSR2 ;open-sample JCN2
	DUP2 ;dict/rom-ext ;has-ext JSR2 ;open-load JCN2
	DUP2 #0005 ADD2 ;dict/theme-ext ;scmp JSR2 ,open-theme JCN
	DUP2 #0005 ADD2 ;is-binary JSR2 ,open-hexa JCN
	;open-text JSR2

JMP2r
	&no-file POP2 JMP2r

@open-folder ( file* -- )

	#0005 ADD2 ;push-dir JSR2

JMP2r

@open-hexa ( file* -- )

	POP2

JMP2r

@open-text ( file* -- )

	#0005 ADD2 ;app-text #3023 #001d #0034 ;add-win JSR2

JMP2r

@open-font ( file* -- )

	#0005 ADD2 ;app-font #2222 #001d #0034 ;add-win JSR2
	;center-win JSR2

JMP2r

@open-sample ( file* -- )

	#0005 ADD2 ;app-play #220f #001d #0034 ;add-win JSR2
	;center-win JSR2

JMP2r

@open-theme ( file* -- )

	#0005 ADD2 ;make-src JSR2 ;load-theme JSR2
	;open-tile JSR2
	;open-color JSR2

JMP2r

@open-load ( file* -- )

	#0005 ADD2 ;make-src JSR2 .File/name DEO2
	#fe00 .File/length DEO2
	;loader-rom #ffd5 #002a ;mcpy JSR2
	;clear-screen JSR2
	POP POP2r
	#ffd5 JMP2

JMP2r

@open-pict ( file* -- )

	#0005 ADD2
	DUP2 ;read-pict-size JSR2
	;app-pict SWP2 INC INC #0000 DUP2 ;add-win JSR2
	;center-win JSR2

JMP2r

@open-as-data ( -- )

	( TODO: create hex editor )

JMP2r

@open-as-text ( -- )

	;draw-item-text/sel LDA ;get-file JSR2 ;open-text JSR2

JMP2r

@open-as-pict ( -- )

	;draw-item-text/sel LDA ;get-file JSR2

	LDAk LIT '- EQU ,&invalid JCN
	LDAk LIT '? EQU ,&invalid JCN

	;open-pict JSR2
	;center-win JSR2

JMP2r
	&invalid
		#0005 ADD2 ;dict/open SWP2 ;add-err JSR2
	JMP2r

@open-as-font ( -- )

	;draw-item-text/sel LDA ;get-file JSR2

	LDAk LIT '- EQU ,&invalid JCN
	LDAk LIT '? EQU ,&invalid JCN

	;open-font JSR2
	;center-win JSR2

JMP2r
	&invalid
		#0005 ADD2 ;dict/open SWP2 ;add-err JSR2
	JMP2r

@open-as-sound ( -- )

JMP2r

@open-about ( -- )

	( unique )
	;app-about ;find-win JSR2
		DUP #ff NEQ ,&reselect JCN POP
	;no-name ;app-about #1613 #0000 DUP2 ;add-win JSR2
	;center-win JSR2

JMP2r
	&reselect ;sel-win JSR2 JMP2r

@open-tile ( -- )

	;patt-chr ;paint-patt/addr STA2
	( unique )
	;app-tile ;find-win JSR2
		DUP #ff NEQ ,&reselect JCN POP
	( create )
	;no-name ;app-tile #0a0f
	( y ) .Screen/height DEI2 #0088 SUB2
	( x ) .Screen/width DEI2 #0060 SUB2
		;add-win JSR2

JMP2r
	&reselect ;sel-win JSR2 JMP2r

@open-color ( -- )

	( unique )
	;app-color ;find-win JSR2
		DUP #ff NEQ ,&reselect JCN POP
	;no-name ;app-color #0a0f
	( y ) .Screen/height DEI2 #0088 SUB2
	( x ) .Screen/width DEI2 #00c0 SUB2
		;add-win JSR2

JMP2r
	&reselect ;sel-win JSR2 JMP2r

@make-dst ( file* -- abs* )

	;buf/dst ,make-path JSR

JMP2r

@make-src ( file* -- abs* )

	;buf/src

@make-path ( file* -- abs* )

	STH2k #0040 ;mclr JSR2
	;buf/loc STH2kr ;scat JSR2
	LIT '/ STH2kr ;sput JSR2
	STH2kr ;scat JSR2
	STH2r

JMP2r

@read-pict-size ( filename* -- size* )

	;scap JSR2 #0009 SUB2 ;read-size JSR2

	ORAk ,&continue JCN
		POP2 #1010
		&continue

JMP2r

( file operations )

@file-create ( -- )

	;dict/create ;dict/newfile-txt ;&callback ;add-form JSR2

JMP2r

&callback ( -- )

	;buf/form ;make-src JSR2 .File/name DEO2
	#0001 .File/length DEO2
	;&buf .File/write DEO2

JMP2r
	&buf 00

@file-rename ( -- )

	;file-validate JSR2 #0005 ADD2 DUP2 ;&src STA2
	;dict/rename SWP2 ;&callback ;add-form JSR2

JMP2r

&callback ( -- )

	( a* ) [ LIT2 &src $2 ] ;make-src JSR2
	( b* ) ;buf/form ;make-dst JSR2
		;fcpy JSR2
	( a* ) ,&src LDR2 ;make-src JSR2
		;fdel JSR2

JMP2r

@file-clone ( -- )

	;file-validate JSR2 #0005 ADD2 DUP2 ;&src STA2
	;dict/clone SWP2 ;&callback ;add-form JSR2

JMP2r

&callback ( -- )

	( a* ) [ LIT2 &src $2 ] ;make-src JSR2
	( b* ) ;buf/form ;make-dst JSR2
		;fcpy JSR2

JMP2r

@file-delete ( -- )

	;file-validate JSR2 #0005 ADD2 DUP2 ;&src STA2
	;dict/delete SWP2 ;&callback ;add-option JSR2

JMP2r

&callback ( -- )

	( a* ) [ LIT2 &src $2 ] ;make-src JSR2 ;fdel JSR2

JMP2r

@file-validate ( -- file* )

	;draw-item-text/sel LDA ;get-file JSR2
		DUP2 #ffff EQU2 ,&fail JCN
		LDAk LIT '- EQU ,&fail JCN
		LDAk LIT '? EQU ,&fail JCN
	( -> do file operations )

JMP2r
	&fail POP2r #0005 ADD2 ;dict/rename SWP2 ;add-err JSR2 JMP2r

( etc )

@is-binary ( file* -- flag )

	.File/name DEO2
	#0001 .File/length DEO2
	&s
		;&buf .File/read DEO2
		[ LIT &buf $1 ] #7e GTH ,&true JCN
		.File/success DEI2 #0000 EQU2 ,&end JCN
		,&s JMP
	&end
	#00

JMP2r
	&true #01 JMP2r

( helpers )

@fcpy ( src* dst* -- )

	.Disk/name DEO2 #0001 .Disk/length DEO2
	.File/name DEO2 #0001 .File/length DEO2
	;&b
	&stream
		DUP2 .File/read DEO2
		.File/success DEI2 ORA ,&continue JCN
			POP2 JMP2r
			&continue
		DUP2 .Disk/write DEO2
		,&stream JMP

JMP2r
	&b $1

@fdel ( src* -- )

	.File/name DEO2
	#01 .File/delete DEO

JMP2r

@mswp ( len* a* b* -- )

	,&b STR2 ,&a STR2
	#0000
	&l
		DUP2 [ LIT2 &a $2 ] ADD2 LDAk STH
		OVR2 [ LIT2 &b $2 ] ADD2 LDAk STH
		SWPr
		STHr ROT ROT STA
		STHr ROT ROT STA
		INC2 GTH2k ,&l JCN
	POP2 POP2

JMP2r

@read-size ( 00x00* -- w h )

	DUP2 ,sbyte JSR ,&w STR
	INC2 INC2 LDAk LIT 'x NEQ ,&cancel JCN
	INC2 ,sbyte JSR [ LIT &w $1 ] SWP

JMP2r
	&cancel POP2 #0000 JMP2r

@sbyte ( str* -- byte )

	LDAk ,chex JSR #40 SFT STH
	INC2 LDA ,chex JSR STHr ADD

JMP2r

@shex ( str* -- val* )

	LIT2r 0000
	&w
		LITr 40 SFT2r
		LITr 00
		LDAk ,chex JSR STH ADD2r
		INC2 LDAk ,&w JCN
	POP2
	STH2r

JMP2r

@chex ( char -- hex )

	DUP #2f GTH OVR #3a LTH AND ,&number JCN
	DUP #60 GTH OVR #67 LTH AND ,&lc JCN
	DUP #40 GTH OVR #47 LTH AND ,&uc JCN
		POP #00 JMP2r
	&number #30 SUB JMP2r
	&uc #37 SUB JMP2r
	&lc #57 SUB

JMP2r

@get-chr ( addr* pixel -- color )

	STH DUP2 STHkr ,get-icn JSR
	ROT ROT #0008 ADD2 STHr ,get-icn JSR DUP ADD ORA

JMP2r

@get-icn ( addr* pixel -- color )

	( y ) STHk #00 SWP #03 SFT2 ADD2 LDA
	( x ) STHr #07 AND #07 SWP SUB SFT #01 AND

JMP2r

@get-strw ( str* -- width* )

	;slen JSR2 #30 SFT2

JMP2r

@rel-mouse ( x* y* win* -- x* y* win* )

	STH2k
	( y ) INC2 INC2 LDA2 SUB2 #0012 SUB2
	SWP2
	( x ) STH2kr LDA2 SUB2 #0008 SUB2
	SWP2
	STH2r

JMP2r

@skey ( key buf* len* -- )

	,&len STR2
	STH2 DUP STH2r ROT
	DUP #08 EQU ,&erase JCN
	DUP #20 LTH ,&invalid JCN
	DUP #7e GTH ,&invalid JCN
	POP
	OVR2 ;slen JSR2 [ LIT2 &len $2 ] LTH2 ,&ok JCN
		POP2 POP JMP2r
		&ok
	;sput JSR2

JMP2r
	&erase POP ;spop JSR2 POP JMP2r
	&invalid POP2 POP2 JMP2r

@has-ext ( str* ext* -- flag )

	SWP2 ,get-ext JSR ;scmp JSR2

JMP2r

@get-ext ( str* -- ext* )

	;scap JSR2 #0004 SUB2

JMP2r

@save-theme ( -- )

	;dict/theme-ext ;make-src JSR2 .File/name DEO2
	#0002 .File/length DEO2
	.System/r DEI2 ,&w JSR
	.System/g DEI2 ,&w JSR
	.System/b DEI2 ,&w JSR
	#0010 .File/length DEO2
	;patt-chr .File/write DEO2
	;draw-desktop JSR2

JMP2r
	&w ,&b STR2 ;&b .File/write DEO2 JMP2r
	&b $2

@scmp ( a* b* -- f ) STH2 &l LDAk LDAkr STHr ANDk #00 EQU ,&e JCN NEQk ,&e JCN POP2 INC2 INC2r ,&l JMP &e NIP2 POP2r EQU JMP2r
@pstr ( str* -- ) &w LDAk #18 DEO INC2 LDAk ,&w JCN POP2 JMP2r

@no-name $1

~src/draw.tal
~src/manifest.tal
@size-system-end

@size-desktop-start
	~src/desktop.tal
@size-desktop-end

@size-apps-start
	~src/apps.tal
@size-apps-end

@size-assets-start
	~src/assets.tal
@size-assets-end

@windows $100
(
	00 x*
	02 y*
	04 size*
	06 app*
	08 name*
	0a buf* )

@buf
	&loc '. $3f
	&src $40 ( used by make-src )
	&dst $40 ( used by make-dst )
	&form $40 ( used by app-form )
	&page $100
	&dir $400
	&app
