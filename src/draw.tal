@redraw-all ( -- )

	;draw-menu-bg JSR2

@draw-desktop ( -- )

	;draw-wallpaper JSR2
	;draw-home JSR2
	( draw windows )
	.length LDZ DUP #00 EQU ,&empty JCN #00
	&loop
		DUP ;get-win JSR2 ;draw-win JSR2
		INC GTHk ,&loop JCN
	POP2

JMP2r
	&empty POP JMP2r

@draw-menu-bg ( -- )

	#0000 DUP2 .Screen/x DEO2 .Screen/y DEO2
	;menu-chr .Screen/addr DEO2
	#15 .Screen/auto DEO
	.Screen/width DEI2 #03 SFT2 NIP #00
	&l
		;menu-chr .Screen/addr DEO2
		#81 .Screen/sprite DEO
		INC GTHk ,&l JCN
	POP2
	;draw-clock JSR2

JMP2r

@draw-win ( win* -- )

	( get anchor )
	LDA2k ,&x STR2 [ LIT2 &x $2 ] .Screen/x DEO2
	INC2k INC2 LDA2 ,&y STR2 [ LIT2 &y $2 ] .Screen/y DEO2

	DUP2 #0004 ADD2
	LDA2k ;draw-win-decor JSR2 INC2 INC2
	( draw name )
	#05 ;draw-chr/color STA
	#20 ;draw-chr JSR2
	( app/manifest ) LDA2k STH2k
	( app/manifest/name ) LDA2 INC2 ;draw-text JSR2
	#20 ;draw-chr JSR2
	INC2 INC2
	( file name )
	#0a ;draw-chr/color STA
	LDA2 ;draw-text JSR2

	( on-draw )
	,&x LDR2 #0008 ADD2 .Screen/x DEO2
	,&y LDR2 #0012 ADD2 .Screen/y DEO2
	STH2r INC2 INC2 LDA2 JMP2

JMP2r

@draw-win-decor ( w h -- )

	STH2
	.Screen/x DEI2 .Screen/y DEI2
	STH2kr ;draw-fill JSR2
	OVR2 OVR2 .Screen/y DEO2 .Screen/x DEO2
	#85 ;draw-frame/color STA
	STH2kr ;frame1-chr ;draw-frame JSR2
	OVR2 OVR2 #000a ADD2 .Screen/y DEO2 .Screen/x DEO2
	STH2r POP ;draw-dotted JSR2
	.Screen/y DEO2 .Screen/x DEO2

JMP2r

@draw-dotted ( w -- )

	#01 .Screen/auto DEO
	;line-icn .Screen/addr DEO2
	#00 &l #0a .Screen/sprite DEO INC GTHk ,&l JCN POP2

JMP2r

@draw-fill ( w h -- )

	#01 .Screen/auto DEO
	;&fill .Screen/addr DEO2
	SWP ,&x STR
	#00
	&h
		[ LIT &x $1 ] #00
		&w
			[ LIT &color 03 ] .Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		;draw-lb JSR2
		.Screen/x DEI2k #00 ,&x LDR #30 SFT2 SUB2 ROT DEO2
		INC GTHk ,&h JCN
	POP2

JMP2r
	&fill ffff ffff ffff ffff

@draw-frame ( w h chr* -- )

	STH2 ,&h STR ,&w STR
	.Screen/x DEI2 DUP2 #0008 SUB2 .Screen/x DEO2
	.Screen/y DEI2 #0008 SUB2 DUP2 .Screen/y DEO2
	( ul ) #00 STH2kr #05 ,&single JSR
	( uu ) [ LIT &w $1 ] #00 STH2kr #0010 ADD2 #01 ,&repeat JSR
	( ur ) #10 STH2kr #06 ,&single JSR
	( rr ) [ LIT &h $1 ] #00 STH2kr #0020 ADD2 #02 ,&repeat JSR
	#0008 ADD2 .Screen/y DEO2
	#0008 SUB2 .Screen/x DEO2
	( ll ) ,&h LDR #10 STH2kr #0020 ADD2 #02 ,&repeat JSR
	( dl ) #20 STH2kr #01 ,&single JSR
	( bb ) ,&w LDR #20 STH2kr #0010 ADD2 #01 ,&repeat JSR
	( dr ) #30 STH2r #00 ,&single JSR

JMP2r
	&repeat ( times color addr* auto -- )
		.Screen/auto DEO
		.Screen/addr DEO2
		STH
		#00 &l STHkr ,&paint JSR INC GTHk ,&l JCN POP2
		POPr
	JMP2r
	&single ( color addr* auto -- )
		.Screen/auto DEO
		.Screen/addr DEO2
	&paint ( mask -- )
		[ LIT &color 85 ] SWP ORA .Screen/sprite DEO
	JMP2r

@draw-icon ( addr* -- )

	.Screen/addr DEO2
	#26 .Screen/auto DEO
	#85 .Screen/sprite DEOk DEOk DEO

JMP2r

@draw-swatches ( -- )

	#05 .Screen/auto DEO
	;swatch-icns/fill .Screen/addr DEO2
	#0c .Screen/sprite DEOk DEO
	;swatch-icns/fill .Screen/addr DEO2
	#0d .Screen/sprite DEOk DEO
	;swatch-icns/fill .Screen/addr DEO2
	#0e .Screen/sprite DEOk DEO
	;swatch-icns/line .Screen/addr DEO2
	#0e .Screen/sprite DEOk DEO

	.Screen/x DEI2k #0040 SUB2 ROT DEO2
	.Screen/y DEI2k #0010 ADD2 ROT DEO2

JMP2r

@draw-slider ( value name* -- )

	( TODO: Add name and write value )

	.Screen/x DEI2 SWP2
	;draw-text JSR2
	.Screen/x DEO2
	;draw-lb JSR2

	#00 .Screen/auto DEO
	STH
	#1000
	&loop
		#00 OVR STHkr GTH #30 SFT ;slider-icns ADD2 .Screen/addr DEO2
		#0a .Screen/sprite DEO
		.Screen/x DEI2k #0004 ADD2 ROT DEO2
		INC GTHk ,&loop JCN
	POP2
	POPr

	.Screen/x DEI2k #0040 SUB2 ROT DEO2
	.Screen/y DEI2k #0010 ADD2 ROT DEO2

JMP2r

@draw-progbar ( den num -- )

	STH
	#00 .Screen/auto DEO
	#00
	&loop
		DUP STHkr LTH STH
		;vert-icn ;check-icn STHr POP2? .Screen/addr DEO2
		.Screen/x DEI2k INC2 INC2 ROT DEO2
		#0a .Screen/sprite DEO
		INC GTHk ,&loop JCN
	POP2
	POPr

JMP2r

@draw-scrollbar ( h -- )

	#01 SUB
	#06 .Screen/auto DEO
	;arrow-icns/v .Screen/addr DEO2
	#0e .Screen/sprite DEO
	#02 .Screen/auto DEO
	#00
	&l
		#0e .Screen/sprite DEO
		INC GTHk ,&l JCN
	POP
	#01 .Screen/auto DEO
	;arrow-icns/v .Screen/addr DEO2
	#2e .Screen/sprite DEO

	( recover y )
	INC #00 SWP #30 SFT2 STH2
	.Screen/y DEI2k STH2r SUB2 ROT DEO2

JMP2r

@draw-drag ( win* color -- )

	;draw-frame/color STA
	,set-anchor JSR
	INC2 INC2 LDA2 ;frame2-chr ;draw-frame JSR2

JMP2r

@set-anchor ( win* -- win/size* )

	LDA2k .Screen/x DEO2 INC2 INC2
	LDA2k .Screen/y DEO2

JMP2r

@set-anchor-body ( win* -- win/size* )

	LDA2k #0008 ADD2 .Screen/x DEO2 INC2 INC2
	LDA2k #0012 ADD2 .Screen/y DEO2

JMP2r

@draw-image ( name* w h -- )

	POP ,&w STR
	.Screen/x DEI2 ,&x STR2
	.File/name DEO2
	[ LIT2 &length 0008 ] .File/length DEO2
	;&buf .Screen/addr DEO2
	#01 .Screen/auto DEO
	#00
	&stream
		;&buf .File/read DEO2
		.File/success DEI2 #0000 EQU2 ,&end JCN
		[ LIT &color 0e ] .Screen/sprite DEO
		INC DUP [ LIT &w $1 ] LTH ,&no-line JCN
			[ LIT2 &x $2 ] .Screen/x DEO2
			;draw-lb JSR2
			POP #00
			&no-line
		,&stream JMP
	&end
	POP

JMP2r
	&buf $10

@draw-dec ( short* -- )

	#01 .Screen/auto DEO
	#00 ,&z STR
	#2710 ,&parse JSR
	#03e8 ,&parse JSR
	#0064 ,&parse JSR
	#000a ,&parse JSR
	NIP
	&emit
		DUP [ LIT &z $1 ] EQU ,&skip JCN
		#ff ,&z STR DUP #30 ADD ;draw-chr JSR2
		&skip
	POP

JMP2r
	&parse
		DIV2k DUP ,&emit JSR MUL2 SUB2
	JMP2r

@draw-list ( str* id -- )

	,&t STR
	.Screen/x DEI2 ,&x STR2

	LDAk #00 EQU ,&end JCN
	#01 .Screen/auto DEO
	LITr 00
	&while
		[ LIT2 &x $2 ] .Screen/x DEO2
		#02 #0e [ LIT &t $1 ] STHkr EQU POP? ;draw-chr/color STA
		;draw-line JSR2
		INCr
		INC2 LDAk ,&while JCN
	&end
	POP2
	POPr

JMP2r

@draw-textarea ( str* w h -- )

	.Screen/x DEI2k #0008 SUB2 ROT DEO2
	#20 ;draw-scrollbar JSR2
	.Screen/x DEI2k #0008 ADD2 ROT DEO2

	#00 SWP #30 SFT2 .Screen/y DEI2 ADD2 ,&y-bound STR2
	#01 SUB #00 SWP #30 SFT2 .Screen/x DEI2 ADD2 ,&x-bound STR2
	.Screen/x DEI2 ,&x STR2

	LDAk #00 EQU ,&end JCN
	#01 .Screen/auto DEO
	&while
		( lb )
		LDAk #0a NEQ ,&no-lb JCN
			[ LIT2 &x $2 ] .Screen/x DEO2
			;draw-lb JSR2
			,&resume JMP
			&no-lb
		( x )
		.Screen/x DEI2 [ LIT2 &x-bound $2 ] GTH2 ,&ok-x JCN
			LDAk ,draw-chr JSR
			&ok-x
		.Screen/y DEI2 [ LIT2 &y-bound $2 ] GTH2 ,&end JCN
		&resume
		INC2 LDAk ,&while JCN
	&end
	POP2

JMP2r

@draw-chr ( char -- )

	#00 SWP #30 SFT2 ;font ADD2 .Screen/addr DEO2
	[ LIT &color 03 ] .Screen/sprite DEO

JMP2r

@draw-text ( str* -- )

	#01 .Screen/auto DEO
	;draw-str JSR2 POP2

JMP2r

@draw-line ( str* -- str* )

	#01 .Screen/auto DEO
	&w
		LDAk #00 EQU ,&end JCN
		LDAk ;draw-chr JSR2
		INC2 LDAk #0a NEQ ,&w JCN
	&end

@draw-lb ( -- )

	.Screen/y DEI2k #0008 ADD2 ROT DEO2

JMP2r

@draw-short ( short* -- )

	SWP ,draw-byte JSR

@draw-byte ( byte -- )

	DUP #04 SFT ,draw-hex JSR

@draw-hex ( char -- )

	#0f AND DUP #09 GTH #07 MUL ADD #30 ADD ;draw-chr JSR2

JMP2r
