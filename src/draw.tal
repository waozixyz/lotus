@redraw-all ( -- )

	;draw-menu-bg JSR2

@draw-desktop ( -- )

	;draw-wallpaper JSR2
	;draw-home JSR2

@draw-windows ( -- )

	.length LDZ DUP #00 EQU ,&empty JCN #00
	&loop
		DUP ;get-win JSR2 ;draw-win JSR2
		INC GTHk ,&loop JCN
	POP2

JMP2r
	&empty POP JMP2r

@draw-menu-bg ( -- )

	#0000 DUP2 .Screen/x DEO2 .Screen/y DEO2
	;menu-chr .Screen/addr DEO2
	#15 .Screen/auto DEO
	.Screen/width DEI2 #03 SFT2 NIP #00
	&l
		;menu-chr .Screen/addr DEO2
		#81 .Screen/sprite DEO
		INC GTHk ,&l JCN
	POP2
	;draw-clock JSR2

JMP2r

@disk1-txt "Potato $1

@draw-win ( win* -- )

	DUP2
	LDA2k .Screen/x DEO2 INC2 INC2
	LDA2k .Screen/y DEO2 INC2 INC2
	LDA2k ;draw-win-decor JSR2 INC2 INC2
	( draw name )
	#05 ;draw-chr/color STA
	#20 ;draw-chr JSR2
	( app/manifest ) LDA2k STH2k
	( app/manifest/name ) LDA2 INC2 ;draw-str JSR2 POP2
	#20 ;draw-chr JSR2
	INC2 INC2
	( file name )
	#0a ;draw-chr/color STA
	LDA2 ;draw-str JSR2 POP2
	STH2r #0002 ADD2 LDA2 JMP2

JMP2r

@draw-win-decor ( w h -- )

	STH2
	.Screen/x DEI2 .Screen/y DEI2
	STH2kr ;draw-fill JSR2
	OVR2 OVR2 .Screen/y DEO2 .Screen/x DEO2
	#85 ;draw-frame/color STA
	STH2kr ;frame1-chr ;draw-frame JSR2
	OVR2 OVR2 #0008 ADD2 .Screen/y DEO2 .Screen/x DEO2
	STH2r POP ;draw-dotted JSR2
	.Screen/y DEO2 .Screen/x DEO2

JMP2r

@draw-dotted ( w -- )

	#01 .Screen/auto DEO
	;line-icn .Screen/addr DEO2
	#00 &l #0a .Screen/sprite DEO INC GTHk ,&l JCN POP2

JMP2r

@draw-fill ( w h -- )

	#01 .Screen/auto DEO
	;&fill .Screen/addr DEO2
	SWP ,&x STR
	#00
	&h
		[ LIT &x $1 ] #00
		&w
			[ LIT &color 03 ] .Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		.Screen/y DEI2k #0008 ADD2 ROT DEO2
		.Screen/x DEI2k #00 ,&x LDR #30 SFT2 SUB2 ROT DEO2
		INC GTHk ,&h JCN
	POP2

JMP2r
	&fill ffff ffff ffff ffff

@draw-frame ( w h chr* -- )

	STH2 ,&h STR ,&w STR
	.Screen/x DEI2 DUP2 #0008 SUB2 .Screen/x DEO2
	.Screen/y DEI2 #0008 SUB2 DUP2 .Screen/y DEO2
	( ul ) #00 STH2kr #05 ,&single JSR
	( uu ) [ LIT &w $1 ] #00 STH2kr #0010 ADD2 #01 ,&repeat JSR
	( ur ) #10 STH2kr #06 ,&single JSR
	( rr ) [ LIT &h $1 ] #00 STH2kr #0020 ADD2 #02 ,&repeat JSR
	#0008 ADD2 .Screen/y DEO2
	#0008 SUB2 .Screen/x DEO2
	( ll ) ,&h LDR #10 STH2kr #0020 ADD2 #02 ,&repeat JSR
	( dl ) #20 STH2kr #01 ,&single JSR
	( bb ) ,&w LDR #20 STH2kr #0010 ADD2 #01 ,&repeat JSR
	( dr ) #30 STH2r #00 ,&single JSR

JMP2r
	&repeat ( times color addr* auto -- )
		.Screen/auto DEO
		.Screen/addr DEO2
		STH
		#00 &l STHkr ,&paint JSR INC GTHk ,&l JCN POP2
		POPr
	JMP2r
	&single ( color addr* auto -- )
		.Screen/auto DEO
		.Screen/addr DEO2
	&paint ( mask -- )
		[ LIT &color 85 ] SWP ORA .Screen/sprite DEO
	JMP2r

@draw-wallpaper ( -- )

	#0018 .Screen/y DEO2
	#71 .Screen/auto DEO
	;patt-icn .Screen/addr DEO2
	.Screen/height DEI2 #06 SFT2 NIP INC #00
	&h
		#0008 .Screen/x DEO2
		.Screen/width DEI2 #03 SFT2 NIP #01 SUB #00
		&w
			#03 .Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		.Screen/y DEI2k #0040 ADD2 ROT DEO2
		INC GTHk ,&h JCN
	POP2
	#15 .Screen/auto DEO

JMP2r

@draw-file ( name* icon* -- )

	,draw-icon JSR
	.Screen/x DEI2k #0020 ADD2 ROT DEO2
	.Screen/y DEI2k #0010 SUB2 ROT DEO2
	;draw-str JSR2 POP2

JMP2r

@draw-icon ( addr* -- )

	.Screen/addr DEO2
	#26 .Screen/auto DEO
	#81 .Screen/sprite DEOk DEOk DEO

JMP2r

@draw-clock ( -- )

	#01 .Screen/auto DEO
	#0004 .Screen/y DEO2
	#0a ;draw-chr/color STA
	.Screen/width DEI2 #0038 SUB2 .Screen/x DEO2
	.DateTime/hour DEI ,&d JSR
	LIT ': ;draw-chr JSR2
	.DateTime/minute DEI ,&d JSR

JMP2r
	&d DUP #0a DIV ,&c JSR #0a MOD
	&c #30 ADD ;draw-chr JSR2 JMP2r

@draw-chr ( char -- )

	#20 SUB #00 SWP #30 SFT2 ;font ADD2 .Screen/addr DEO2
	[ LIT &color 03 ] .Screen/sprite DEO

JMP2r

@get-strw ( str* -- width* )

	;slen JSR2 #30 SFT2

JMP2r

@draw-str-lb ( str* -- )

	.Screen/x DEI2 ,&x STR2

	&w
		LDAk #0a EQU ,&lb JCN
		.Screen/x DEI2 [ LIT2 &max $2 ] #0018 SUB2 GTH2 ,&skip JCN
			LDAk ;draw-chr JSR2
			&skip
		INC2 LDAk ,&w JCN
	POP2

JMP2r
	&lb
		[ LIT2 &x $2 ] .Screen/x DEO2
		.Screen/y DEI2k #0008 ADD2 ROT DEO2
		INC2 ,&w JMP

@print-str ( str* -- )

	&w
		LDAk #18 DEO
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@read-size ( 00x00* -- w h )

	DUP2 ;sbyte JSR2 #00 EQU ,&cancel JCN
	INC2k INC2 LDA LIT 'x NEQ ,&cancel JCN
	DUP2 #0003 ADD2 ;sbyte JSR2 #00 EQU ,&cancel JCN
	( y ) DUP2 #0003 ADD2 ;sbyte JSR2 STH
	( x ) ;sbyte JSR2 STHr

JMP2r
	&cancel POP2 #0000 JMP2r

@draw-drag ( win* color -- )

	;draw-frame/color STA
	LDA2k .Screen/x DEO2
	INC2 INC2 LDA2k .Screen/y DEO2
	INC2 INC2 LDA2 ;frame2-chr ;draw-frame JSR2

JMP2r

@draw-image ( name* w h -- )

	POP ,&w STR
	.Screen/x DEI2 ,&x STR2
	.File/name DEO2
	[ LIT2 &length 0008 ] .File/length DEO2
	;&buf .Screen/addr DEO2
	#01 .Screen/auto DEO
	#00
	&stream
		;&buf .File/read DEO2
		[ LIT &color 0e ] .Screen/sprite DEO
		INC DUP [ LIT &w $1 ] LTH ,&no-line JCN
			[ LIT2 &x $2 ] .Screen/x DEO2
			.Screen/y DEI2k #0008 ADD2 ROT DEO2
			POP #00
			&no-line
		.File/success DEI2 ORA ,&stream JCN
	POP

JMP2r
	&buf $10

@draw-dec ( short* -- )

	#00 ,&z STR
	#2710 ,&parse JSR
	#03e8 ,&parse JSR
	#0064 ,&parse JSR
	#000a ,&parse JSR
	NIP
	&emit
		DUP [ LIT &z $1 ] EQU ,&skip JCN
		#ff ,&z STR DUP #30 ADD ;draw-chr JSR2
		&skip
	POP

JMP2r
	&parse
		DIV2k DUP ,&emit JSR MUL2 SUB2
	JMP2r
