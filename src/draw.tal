@redraw-all ( -- )

	;draw-menu-bg JSR2

@draw-desktop ( -- )

	;draw-wallpaper JSR2
	;load-dir JSR2

	( draw icons )
	;get-rows JSR2 STH
	LITr 00
	;buf/dir
	&w
		#00 STHk2r SWP DIV #00a0 MUL2 #0010 ADD2 .Screen/x DEO2
		#00 STHk2r SWP MOD #0018 MUL2 #0018 ADD2 .Screen/y DEO2
		DUP2 ;draw-item-icon JSR2
		DUP2 STHkr ;draw-item-text JSR2
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POP2r

	( draw windows )
	.length LDZ #00 EQUk ,&no-win JCN
	&loop
		DUP ;get-win JSR2 ;draw-win JSR2
		INC GTHk ,&loop JCN
	&no-win
	POP2

JMP2r

@draw-menu-bg ( -- )

	#0000 DUP2 .Screen/x DEO2 .Screen/y DEO2
	;menu-chr .Screen/addr DEO2
	#15 .Screen/auto DEO
	.Screen/width DEI2 #03 SFT2 NIP #00
	&l
		;menu-chr .Screen/addr DEO2
		#81 .Screen/sprite DEO
		INC GTHk ,&l JCN
	POP2
	;draw-clock JSR2

JMP2r

@draw-win ( win* -- )

	( get anchor )
	LDA2k ,&x STR2 [ LIT2 &x $2 ] .Screen/x DEO2
	INC2k INC2 LDA2 ,&y STR2 [ LIT2 &y $2 ] .Screen/y DEO2

	DUP2 #0004 ADD2
	LDA2k ;draw-win-decor JSR2 INC2 INC2
	( draw name )
	#05 ;draw-chr/color STA
	#20 ;draw-chr JSR2
	( app/manifest ) LDA2k STH2k
	( app/manifest/name ) LDA2 INC2 ;draw-text JSR2
	#20 ;draw-chr JSR2
	INC2 INC2
	( file name )
	#0a ;draw-chr/color STA
	LDA2 ;draw-text JSR2

	( on-draw )
	,&x LDR2 #0008 ADD2 .Screen/x DEO2
	,&y LDR2 #0012 ADD2 .Screen/y DEO2
	STH2r INC2 INC2 LDA2 JMP2

JMP2r

@draw-win-decor ( w h -- )

	STH2
	.Screen/x DEI2 .Screen/y DEI2
	STH2kr ;draw-fill JSR2
	OVR2 OVR2 .Screen/y DEO2 .Screen/x DEO2
	#85 ;draw-frame/color STA
	STH2kr ;frame1-chr ;draw-frame JSR2
	OVR2 OVR2 #000a ADD2 .Screen/y DEO2 .Screen/x DEO2
	STH2r POP #0a ,draw-dotted JSR
	.Screen/y DEO2 .Screen/x DEO2

JMP2r

@draw-dotted ( w color -- )

	.Screen/x DEI2 SWP2
	,&color STR
	#01 .Screen/auto DEO
	;line-icn .Screen/addr DEO2
	#00 &l [ LIT &color 0a ] .Screen/sprite DEO INC GTHk ,&l JCN POP2
	.Screen/x DEO2

JMP2r

@draw-wallpaper ( -- )

	#0000 .Screen/x DEO2
	#0010 .Screen/y DEO2
	#01 .Screen/auto DEO
	;patt-chr .Screen/addr DEO2
	;dict/paper-ext .File/name DEO2
	#0008 .File/length DEO2

	&stream
		;patt-chr .File/read DEO2
		.File/success DEI2 #0000 EQU2 ,&eof JCN
		#01 .Screen/sprite DEO
		.Screen/x DEI2 .Screen/width DEI2 LTH2 ,&no-lb JCN
			#0000 .Screen/x DEO2
			.Screen/y DEI2k #0008 ADD2 ROT DEO2
			&no-lb
		,&stream JMP
		&eof
	.Screen/y DEI2 .Screen/height DEI2 LTH2 ,draw-patt JCN

JMP2r

@draw-patt ( -- )

	( cache size )
	.Screen/width DEI2 #03 SFT2 NIP ,&x STR
	.Screen/height DEI2 #03 SFT2 NIP INC ,&y STR
	( draw )
	#0010 .Screen/y DEO2
	#01 .Screen/auto DEO
	;patt-chr .Screen/addr DEO2
	[ LIT &y $1 ] #00
	&h
		STHk
		#0000 .Screen/x DEO2
		[ LIT &x $1 ] #00
		&w
			DUP #01 AND #40 SFT STHkr #01 AND #50 SFT ORA #81 ORA
				.Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		POPr
		.Screen/y DEI2k #0008 ADD2 ROT DEO2
		INC GTHk ,&h JCN
	POP2

JMP2r

@scroll-text ( str* w -- str* w )

	STHk ROT ROT DUP2
	;slen JSR2 #00 STHr SUB2 DUP2 #8000 LTH2 ,&no-scroll JCN
		POP2 #0000 &no-scroll
	ADD2 ROT

JMP2r

@draw-input ( str* w -- )

	.Screen/x DEI2 ,&x STR2
	.Screen/y DEI2 ,&y STR2

	,scroll-text JSR

	DUP ;input-icns ;draw-capped JSR2
	#30 SFT #00 SWP #000e ADD2 STH2
	.Screen/x DEI2k STH2r SUB2 ROT DEO2
	.Screen/y DEI2k #0004 ADD2 ROT DEO2

	#0a ;draw-chr/color STA
	;draw-str JSR2 POP2

	( re-anchor )
	[ LIT2 &x $2 ] .Screen/x DEO2
	[ LIT2 &y $2 ] #0018 ADD2 .Screen/y DEO2

JMP2r

@draw-button ( str* w -- )

	DUP ;button-icns ,draw-capped JSR
	( button center )
	#31 SFT #00 SWP STH2
	( string center )
	DUP2 ;slen JSR2 #30 SFT #01 SFT #0008 ADD2 STH2 ADD2r

	.Screen/x DEI2k STH2r SUB2 ROT DEO2
	.Screen/y DEI2k #0004 ADD2 ROT DEO2

	#0f ;draw-chr/color STA
	;draw-str JSR2 POP2

JMP2r

@draw-capped ( w sprite* -- )

	#15 .Screen/auto DEO
	DUP2 .Screen/addr DEO2 #0010 ADD2
	#0a .Screen/sprite DEO
	ROT #00
	&l
		OVR2 .Screen/addr DEO2
		#0a .Screen/sprite DEO
		INC GTHk ,&l JCN
	POP2
	#15 .Screen/auto DEO
	#0010 ADD2 .Screen/addr DEO2
	#0a .Screen/sprite DEO

JMP2r

@draw-fill ( w h -- )

	#01 .Screen/auto DEO
	;fill-icn .Screen/addr DEO2
	SWP ,&x STR
	#00
	&h
		[ LIT &x $1 ] #00
		&w
			[ LIT &color 03 ] .Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		;draw-lb JSR2
		.Screen/x DEI2k #00 ,&x LDR #30 SFT2 SUB2 ROT DEO2
		INC GTHk ,&h JCN
	POP2

JMP2r

@draw-frame ( w h chr* -- )

	STH2 ,&h STR ,&w STR
	.Screen/x DEI2 DUP2 #0008 SUB2 .Screen/x DEO2
	.Screen/y DEI2 #0008 SUB2 DUP2 .Screen/y DEO2
	( ul ) #00 STH2kr #05 ,&single JSR
	( uu ) [ LIT &w $1 ] #00 STH2kr #0010 ADD2 #01 ,&repeat JSR
	( ur ) #10 STH2kr #06 ,&single JSR
	( rr ) [ LIT &h $1 ] #00 STH2kr #0020 ADD2 #02 ,&repeat JSR
	#0008 ADD2 .Screen/y DEO2
	#0008 SUB2 .Screen/x DEO2
	( ll ) ,&h LDR #10 STH2kr #0020 ADD2 #02 ,&repeat JSR
	( dl ) #20 STH2kr #01 ,&single JSR
	( bb ) ,&w LDR #20 STH2kr #0010 ADD2 #01 ,&repeat JSR
	( dr ) #30 STH2r #00 ,&single JSR

JMP2r
	&repeat ( times color addr* auto -- )
		.Screen/auto DEO
		.Screen/addr DEO2
		STH
		#00 &l STHkr ,&paint JSR INC GTHk ,&l JCN POP2
		POPr
	JMP2r
	&single ( color addr* auto -- )
		.Screen/auto DEO
		.Screen/addr DEO2
	&paint ( mask -- )
		[ LIT &color 85 ] SWP ORA .Screen/sprite DEO
	JMP2r

@draw-icon ( addr* -- )

	.Screen/addr DEO2
	#26 .Screen/auto DEO
	#85 .Screen/sprite DEOk DEOk DEO

JMP2r

@draw-icon-size ( size* name* icon* -- )

	;draw-icon JSR2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2
	.Screen/x DEI2k STH2k #0020 ADD2 ROT DEO2
	[ LIT &c1 0a ] ;draw-chr/color STA
	;draw-text JSR2
	STH2r #0020 ADD2 .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2
	[ LIT &c2 05 ] ;draw-chr/color STA
	;draw-dec JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

JMP2r

@draw-item-icon ( ptr* -- ptr* )

	LDAk LIT '- EQU ,&folder JCN
	LDAk LIT '? EQU ,&unknown JCN
	DUP2 #0005 ADD2 LDA LIT '. EQU ,&unknown JCN
	DUP2 #0005 ADD2 ;dict/rom-ext ;has-ext JSR2 ,&rom JCN
	DUP2 #0005 ADD2 ;dict/chr-ext ;has-ext JSR2 ,&picture JCN
	DUP2 #0005 ADD2 ;dict/icn-ext ;has-ext JSR2 ,&picture JCN
	DUP2 #0005 ADD2 ;dict/pcm-ext ;has-ext JSR2 ,&sound JCN
	DUP2 #0005 ADD2 ;dict/uf2-ext ;has-ext JSR2 ,&font JCN
	POP2 ;icons/text ;draw-icon JSR2

JMP2r
	&folder POP2 ;icons/folder ;draw-icon JSR2 JMP2r
	&unknown POP2 ;icons/unknown ;draw-icon JSR2 JMP2r
	&picture POP2 ;icons/picture ;draw-icon JSR2 JMP2r
	&rom POP2 ;icons/application ;draw-icon JSR2 JMP2r
	&sound POP2 ;icons/sound ;draw-icon JSR2 JMP2r
	&font POP2 ;icons/font ;draw-icon JSR2 JMP2r

@draw-item-text ( ptr* id -- ptr* )

	[ LIT &sel 00 ] EQU #09 MUL INC INC ;draw-chr/color STA

	LDAk LIT '- EQU ;&no-size JCN2
	LDAk LIT '? EQU ;&no-size JCN2

	.Screen/x DEI2k #0020 ADD2 STH2k ROT DEO2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2

	#0005 ADD2 ;draw-text JSR2
	STH2r .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2

	#01 ;draw-chr/color STA
	DUP2 ;&buf #0004 ;mcpy JSR2
	;&buf ;shex JSR2 ;draw-dec JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

JMP2r
	&buf $5
	&no-size
		.Screen/y DEI2k #0010 SUB2 ROT DEO2
		.Screen/x DEI2k #0020 ADD2 ROT DEO2
		#0005 ADD2 ;draw-text JSR2
	JMP2r

@draw-swatches ( -- )

	#05 .Screen/auto DEO
	;swatch-icns/fill .Screen/addr DEO2
	#0c .Screen/sprite DEOk DEO
	;swatch-icns/fill .Screen/addr DEO2
	#0d .Screen/sprite DEOk DEO
	;swatch-icns/fill .Screen/addr DEO2
	#0e .Screen/sprite DEOk DEO
	;swatch-icns/line .Screen/addr DEO2
	#0e .Screen/sprite DEOk DEO

	.Screen/x DEI2k #0040 SUB2 ROT DEO2
	.Screen/y DEI2k #0010 ADD2 ROT DEO2

JMP2r

@draw-waveform ( addr* length* zoom* -- )

	.Screen/x DEI2 ,&x STR2
	.Screen/y DEI2 ,&y STR2

	( draw mid )
	.Screen/y DEI2k #001f ADD2 ROT DEO2
	#20 #0c ;draw-dotted JSR2

	( draw wav )
	,&zoom STR2
	#01 .Screen/auto DEO
	LITr 00
	ADD2k NIP2 SWP2
	&loop
		LDAk #00 SWP #02 SFT2 [ LIT2 &y $2 ] ADD2 .Screen/y DEO2
		#0a .Screen/pixel DEO
		INCr STHkr #00 EQU ,&end JCN
		[ LIT2 &zoom $2 ] ADD2 GTH2k ,&loop JCN
	&end
	POP2 POP2
	POPr

	( rewind )
	[ LIT2 &x $2 ] .Screen/x DEO2
	,&y LDR2 #0048 ADD2 .Screen/y DEO2

JMP2r

@draw-knob ( x* y* value -- )

	STH
	OVR2 OVR2 .Screen/y DEO2 .Screen/x DEO2
	( circle )
	;knob-icns .Screen/addr DEO2
	#16 .Screen/auto DEO
	#0e .Screen/sprite DEOk DEO
	#00 .Screen/auto DEO
	( value )
	#0010 ADD2 .Screen/y DEO2
	#0004 ADD2 .Screen/x DEO2
	STHkr ;draw-hex JSR2
	#0e .Screen/sprite DEO
	( marker )
	.Screen/x DEI2 #0004 SUB2 #00 #00 STHkr ;knob-offsetx ADD2 LDA ADD2 .Screen/x DEO2
	.Screen/y DEI2 #0010 SUB2 #00 #00 STHr ;knob-offsety ADD2 LDA ADD2 .Screen/y DEO2
	;knob-icns #0020 ADD2 .Screen/addr DEO2
	#0a .Screen/sprite DEO

JMP2r

@draw-octave ( -- )

	.Screen/x DEI2
	.Screen/y DEI2

	#02 .Screen/auto DEO
	OVR2 #0048 ADD2 .Screen/x DEO2
	;arrow-icns/v .Screen/addr DEO2
	DUP2 .Screen/y DEO2
	#0e .Screen/sprite DEO

	[ LIT &octave 03 ] ;draw-hex JSR2

	;arrow-icns/v .Screen/addr DEO2
	DUP2 #0010 ADD2 .Screen/y DEO2
	#2e .Screen/sprite DEO

	.Screen/y DEO2
	.Screen/x DEO2
	#25 .Screen/auto DEO
	[ LIT &last ff ]
	DUP #00 EQU ;octave-icns/a ,draw-key JSR
	DUP #02 EQU ;octave-icns/b ,draw-key JSR
	DUP #04 EQU ;octave-icns/c ,draw-key JSR
	DUP #05 EQU ;octave-icns/a ,draw-key JSR
	DUP #07 EQU ;octave-icns/b ,draw-key JSR
	DUP #09 EQU ;octave-icns/b ,draw-key JSR
	#0b EQU ;octave-icns/c ,draw-key JSR
	#00 ;octave-icns/d ,draw-key JSR

JMP2r

@draw-key ( color addr* -- )

	.Screen/addr DEO2
	#0b ADD .Screen/sprite DEO

JMP2r

@draw-slider ( value name* -- )

	( TODO: Add name and write value )

	.Screen/x DEI2 SWP2
	;draw-text JSR2
	.Screen/x DEO2
	;draw-lb JSR2

	#00 .Screen/auto DEO
	STH
	#1000
	&loop
		#00 OVR STHkr GTH #30 SFT ;slider-icns ADD2 .Screen/addr DEO2
		#0a .Screen/sprite DEO
		.Screen/x DEI2k #0004 ADD2 ROT DEO2
		INC GTHk ,&loop JCN
	POP2
	POPr

	.Screen/x DEI2k #0040 SUB2 ROT DEO2
	.Screen/y DEI2k #0010 ADD2 ROT DEO2

JMP2r

@draw-progbar ( den num -- )

	STH
	#00 .Screen/auto DEO
	#00
	&loop
		DUP STHkr LTH STH
		;vert-icn ;check-icn STHr [ JMP SWP2 POP2 ] .Screen/addr DEO2
		.Screen/x DEI2k INC2 INC2 ROT DEO2
		#0a .Screen/sprite DEO
		INC GTHk ,&loop JCN
	POP2
	POPr

JMP2r

@draw-scrollbar ( h -- )

	#01 SUB
	#06 .Screen/auto DEO
	;arrow-icns/v .Screen/addr DEO2
	#0e .Screen/sprite DEO
	#02 .Screen/auto DEO
	#00
	&l
		#0e .Screen/sprite DEO
		INC GTHk ,&l JCN
	POP
	#01 .Screen/auto DEO
	;arrow-icns/v .Screen/addr DEO2
	#2e .Screen/sprite DEO

	( recover y )
	INC #00 SWP #30 SFT2 STH2
	.Screen/y DEI2k STH2r SUB2 ROT DEO2

JMP2r

@draw-drag ( win* color -- )

	;draw-frame/color STA
	,set-anchor JSR
	INC2 INC2 LDA2 ;frame2-chr ;draw-frame JSR2

JMP2r

@set-anchor ( win* -- win/size* )

	LDA2k .Screen/x DEO2 INC2 INC2
	LDA2k .Screen/y DEO2

JMP2r

@set-anchor-body ( win* -- win/size* )

	LDA2k #0008 ADD2 .Screen/x DEO2 INC2 INC2
	LDA2k #0012 ADD2 .Screen/y DEO2

JMP2r

( image handlers )

@draw-pict ( name* -- )

	DUP2 ;get-ext JSR2 ;dict/chr-ext ;scmp JSR2 STH
	( toggle 1-bit/2-bit )
	#0010 #0008 STHkr [ JMP SWP2 POP2 ] .File/length DEO2
	[ LIT &2bit 81 ] [ LIT &1bit 0e ] STHr [ JMP SWP POP ] ,&color STR
	DUP2 .File/name DEO2
		;read-pict-size JSR2
	,&height STR ,&width STR
	.Screen/x DEI2 ,&anchor STR2
	;&buf .Screen/addr DEO2
	( draw )
	#01 .Screen/auto DEO
	[ LIT &height $1 ] #00
	&v
		[ LIT2 &anchor $2 ] .Screen/x DEO2
		[ LIT &width $1 ] #00
		&h
			;&buf .File/read DEO2
			[ LIT &color $1 ] .Screen/sprite DEO
			INC GTHk ,&h JCN
		POP2
		;draw-lb JSR2
		INC GTHk ,&v JCN
	POP2

JMP2r
	&buf $10

@draw-dec ( short* -- )

	#01 .Screen/auto DEO
	#00 ,&z STR
	#2710 ,&parse JSR
	#03e8 ,&parse JSR
	#0064 ,&parse JSR
	#000a ,&parse JSR
	#01 ,&z STR NIP
	&emit
		DUP [ LIT &z $1 ] EQU ,&skip JCN
		#ff ,&z STR DUP #30 ADD ;draw-chr JSR2
		&skip
	POP

JMP2r
	&parse
		DIV2k DUP ,&emit JSR MUL2 SUB2
	JMP2r

@draw-textarea ( str* w h -- )

	.Screen/x DEI2k #0008 SUB2 ROT DEO2
	#20 ;draw-scrollbar JSR2
	.Screen/x DEI2k #0008 ADD2 ROT DEO2

	#00 SWP #30 SFT2 .Screen/y DEI2 ADD2 ,&y-bound STR2
	#01 SUB #00 SWP #30 SFT2 .Screen/x DEI2 ADD2 ,&x-bound STR2
	.Screen/x DEI2 ,&x STR2

	LDAk #00 EQU ,&end JCN
	#01 .Screen/auto DEO
	&while
		( lb )
		LDAk #0a NEQ ,&no-lb JCN
			[ LIT2 &x $2 ] .Screen/x DEO2
			;draw-lb JSR2
			,&resume JMP
			&no-lb
		( x )
		.Screen/x DEI2 [ LIT2 &x-bound $2 ] GTH2 ,&ok-x JCN
			LDAk ,draw-chr JSR
			&ok-x
		.Screen/y DEI2 [ LIT2 &y-bound $2 ] GTH2 ,&end JCN
		&resume
		INC2 LDAk ,&while JCN
	&end
	POP2

JMP2r

@draw-chr ( char -- )

	#00 SWP #30 SFT2 ;font ADD2 .Screen/addr DEO2
	[ LIT &color 03 ] .Screen/sprite DEO

JMP2r

@draw-text ( str* -- )

	;draw-str JSR2 POP2

JMP2r

@draw-line ( str* -- )

	.Screen/x DEI2 ,&x STR2
	#01 .Screen/auto DEO
	&w
		LDAk #00 EQU ,&end JCN
		LDAk ;draw-chr JSR2
		INC2 LDAk #0a NEQ ,&w JCN
	&end
	POP2
	[ LIT2 &x $2 ] .Screen/x DEO2

@draw-lb ( -- )

	.Screen/y DEI2k #0008 ADD2 ROT DEO2

JMP2r

@draw-short ( short* -- )

	SWP ,draw-byte JSR

@draw-byte ( byte -- )

	DUP #04 SFT ,draw-hex JSR

@draw-hex ( char -- )

	#0f AND DUP #09 GTH #07 MUL ADD #30 ADD ;draw-chr JSR2

JMP2r

@clear-screen ( -- )

	#01 .Screen/auto DEO
	#0000 .Screen/y DEO2
	.Screen/height DEI2 #03 SFT2 NIP #00
	&h
		#0000 .Screen/x DEO2
		.Screen/width DEI2 #03 SFT2 NIP #00
		&w
			#00 .Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		.Screen/y DEI2k #0008 ADD2 ROT DEO2
		INC GTHk ,&h JCN
	POP2
	#15 .Screen/auto DEO

JMP2r
