@manifest

	03 "Potato $1
		01 '2 :open-disk "Disk 20 "Util. $1
		02 'w :open-look "Wallpaper $1
		01 'q :exit "Exit $1
	04 "File $1
		01 'n :file-create "Create $1
		01 'r :file-rename "Rename $1
		01 'd :file-clone "Clone $1
		01 08 :file-delete "Delete $1
	03 "Open $1
		01 'd :open-as-data "As 20 "Data $1
		01 't :open-as-text "As 20 "Text $1
		01 'p :open-as-pict "As 20 "Pict $1
	04 "Select $1
		10 00 :sel-up "Up $1
		20 00 :sel-down "Down $1
		40 00 :sel-left "Left $1
		80 00 :sel-right "Right $1
	03 "Go $1
		12 00 :go-active "Active $1
		42 00 :go-home "Home $1
		82 00 :go-file "Open $1
	$1

@file-create ( -- )

	;&new-name
		DUP2 ;get-path JSR2 .File/name DEO2
	#0001 .File/length DEO2
	;&buf .File/write DEO2
		;find-name JSR2 ;sel JSR2

JMP2r
	&new-name "NEWFILE $1
	&buf 00

@file-rename ( -- )

JMP2r

@file-clone ( -- )

JMP2r

@file-delete ( -- )

	;draw-item-text/sel LDA ;get-file JSR2 ;get-path JSR2 .File/name DEO2
	#01 .File/delete DEO
	;draw-desktop JSR2

JMP2r

@sel-up ( -- )

	;draw-item-text/sel LDA
		DUP ,&continue JCN POP JMP2r
		&continue #01 SUB ;sel JSR2

JMP2r

@sel-down ( -- )

	;draw-item-text/sel LDA INC ;sel JSR2

JMP2r

@sel-left ( -- )

	;draw-item-text/sel LDA ;get-rows JSR2 SUB ;sel JSR2

JMP2r

@sel-right ( -- )

	;draw-item-text/sel LDA ;get-rows JSR2 ADD ;sel JSR2

JMP2r

@sel ( id -- )

	;draw-item-text/sel STA
	;draw-desktop JSR2

JMP2r

@open-as-data ( -- )

	( TODO: create hex editor )

JMP2r

@open-as-text ( -- )

	;draw-item-text/sel LDA ;get-file JSR2 ;open-text JSR2

JMP2r

@open-as-pict ( -- )

	;draw-item-text/sel LDA ;get-file JSR2 ;open-pict JSR2

JMP2r

@get-file ( id -- <file*> )

	STH
	LITr 00
	;buf/dir
	&w
		EQUkr STHr ,&found JCN
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POP2r
	#ffff

JMP2r
	&found POP2r JMP2r

@find-name ( name* -- <id> )

	,&t STR2
	LITr 00
	;buf/dir
	&w
		#0005 ADD2 DUP2 [ LIT2 &t $2 ] ;scmp JSR2 ,&found JCN
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POPr
	#ff

JMP2r
	&found POP2 STHr JMP2r

@draw-wallpaper ( -- )

	( cache size )
	.Screen/width DEI2 #03 SFT2 NIP ,&x STR
	.Screen/height DEI2 #03 SFT2 NIP INC ,&y STR
	( draw )
	#0010 .Screen/y DEO2
	#01 .Screen/auto DEO
	;patt-chr .Screen/addr DEO2
	[ LIT &y $1 ] #00
	&h
		STHk
		#0000 .Screen/x DEO2
		[ LIT &x $1 ] #00
		&w
			DUP #01 AND #40 SFT STHkr #01 AND #50 SFT ORA #81 ORA
				.Screen/sprite DEO
			INC GTHk ,&w JCN
		POP2
		POPr
		.Screen/y DEI2k #0008 ADD2 ROT DEO2
		INC GTHk ,&h JCN
	POP2

JMP2r

@draw-home ( -- )

	( load dir )
	;buf/loc .File/name DEO2
	#0400 .File/length DEO2

	;buf/dir
		DUP2 .File/read DEO2
		;remove-lb JSR2

	;get-rows JSR2 STH
	LITr 00
	;buf/dir
	&w
		#00 STHk2r SWP DIV #00a0 MUL2 #0010 ADD2 .Screen/x DEO2
		#00 STHk2r SWP MOD #0018 MUL2 #0018 ADD2 .Screen/y DEO2
		DUP2 ;draw-item-icon JSR2
		DUP2 STHkr ;draw-item-text JSR2
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POP2r

JMP2r

@draw-icon-size ( size* name* icon* -- )

	;draw-icon JSR2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2
	.Screen/x DEI2k STH2k #0020 ADD2 ROT DEO2
	[ LIT &c1 0a ] ;draw-chr/color STA
	;draw-text JSR2
	STH2r #0020 ADD2 .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2
	[ LIT &c2 05 ] ;draw-chr/color STA
	;draw-dec JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

JMP2r

@draw-item-icon ( ptr* -- ptr* )

	LDAk LIT '- EQU ;&folder JCN2
	LDAk LIT '? EQU ;&large JCN2
	DUP2 #0005 ADD2 LDA LIT '. EQU ;&hidden JCN2
	LDAk LIT '! EQU ;&potato JCN2
	LDAk LIT '@ EQU ;&disk JCN2
	POP2 ;icons/text ;draw-icon JSR2

JMP2r
	&folder POP2 ;icons/folder ;draw-icon JSR2 JMP2r
	&large POP2 ;icons/unknown ;draw-icon JSR2 JMP2r
	&potato POP2 ;icons/potato ;draw-icon JSR2 JMP2r
	&disk POP2 ;icons/disk ;draw-icon JSR2 JMP2r
	&hidden POP2  ;icons/pict ;draw-icon JSR2 JMP2r

@draw-item-text ( ptr* id -- ptr* )

	[ LIT &sel 00 ] EQU #09 MUL INC INC ;draw-chr/color STA

	LDAk LIT '- EQU ;&folder JCN2
	LDAk LIT '? EQU ;&folder JCN2

	LDAk LIT '! EQU ;&potato JCN2
	LDAk LIT '@ EQU ;&disk JCN2

	.Screen/x DEI2k #0020 ADD2 STH2k ROT DEO2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2

	#0005 ADD2 ;draw-text JSR2
	STH2r .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2

	#01 ;draw-chr/color STA
	DUP2 ;&buf #0004 ;mcpy JSR2
	;&buf ;shex JSR2 ;draw-dec JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

JMP2r
	&buf $5
	&folder
		.Screen/y DEI2k #0010 SUB2 ROT DEO2
		.Screen/x DEI2k #0020 ADD2 ROT DEO2
		#0005 ADD2 ;draw-text JSR2
	JMP2r
	&potato
		.Screen/x DEI2k #0020 ADD2 STH2k ROT DEO2
		.Screen/y DEI2k #0014 SUB2 ROT DEO2
			#0005 ADD2 ;draw-text JSR2
		STH2r .Screen/x DEO2
		.Screen/y DEI2k #0009 ADD2 ROT DEO2
		#01 ;draw-chr/color STA
		#0000 ;buf/app SUB2 ;draw-dec JSR2
		#20 ;draw-chr JSR2
		;dict/free ;draw-text JSR2
	JMP2r
	&disk
		.Screen/x DEI2k #0020 ADD2 STH2k ROT DEO2
		.Screen/y DEI2k #0014 SUB2 ROT DEO2
			#0005 ADD2 ;draw-text JSR2
		STH2r .Screen/x DEO2
		.Screen/y DEI2k #0009 ADD2 ROT DEO2
		#01 ;draw-chr/color STA
		;dict/empty ;draw-text JSR2
	JMP2r

@edit-copy ( -- ) JMP2r
@edit-paste ( -- ) JMP2r
@edit-cut ( -- ) JMP2r
@edit-erase ( -- ) JMP2r
@toggle-zoom ( -- ) JMP2r

@open-disk ( -- )

	;no-name ;app-disk #1613 #0023 #0073 ;add-win JSR2
	;center-win JSR2

JMP2r

@open-look ( -- )

	;patt-chr ;paint-patt/addr STA2
	;no-name ;app-tile #0a0f #0023 #0073 ;add-win JSR2
	;center-win JSR2

JMP2r

@get-rows ( -- rows )

	.Screen/height DEI2 #0018 SUB2 #0018 DIV2 NIP

JMP2r
