@manifest

	04 "Potato $1
		01 '1 :open-about "About $1
		01 'w :open-tile "Wallpaper $1
		01 't :open-color "Theme $1
		01 'q :exit "Exit $1
	04 "File $1
		01 'n :file-create "Create $1
		01 'r :file-rename "Rename $1
		01 'd :file-clone "Clone $1
		01 08 :file-delete "Delete $1
	05 "Open $1
		01 'd :open-as-data "As 20 "Data $1
		01 't :open-as-text "As 20 "Text $1
		01 'p :open-as-pict "As 20 "Pict $1
		01 'p :open-as-font "As 20 "Font $1
		01 'p :open-as-sound "As 20 "Sound $1
	04 "Select $1
		10 00 :sel-up "Up $1
		20 00 :sel-down "Down $1
		40 00 :sel-left "Left $1
		80 00 :sel-right "Right $1
	03 "Go $1
		12 00 :go-active "Active $1
		42 00 :go-home "Home $1
		82 00 :go-file "Open $1
	$1

( window management )

@pick-win ( x* y* -- <id> )

	,&y STR2 ,&x STR2
	#00 .length LDZ DUP #00 EQU ,&skip JCN
	&loop
		DUP #01 SUB #0a MUL .windows ADD
			#00 SWP [ LIT2 &x $2 ] [ LIT2 &y $2 ] ROT2 ,within-win JSR ,&found JCN
			#01 SUB LTHk ,&loop JCN
		&skip
	POP2
	#ff

JMP2r
	&found NIP #01 SUB JMP2r

@within-win ( x* y* win* -- flag )

	STH2
	( x0 ) OVR2 STH2kr LDA2 LTS2 ,&fail JCN
	( y0 ) DUP2 STH2kr INC2 INC2 LDA2 LTS2 ,&fail JCN
	( x1 ) OVR2 STH2kr LDA2k SWP2 #0004 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTS2 ,&fail JCN
	( y1 ) DUP2 STH2kr INC2 INC2 LDA2k SWP2 #0003 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTS2 ,&fail JCN
	POP2 POP2 POP2r #01

JMP2r
	&fail POP2 POP2 POP2r #00 JMP2r

@find-win ( app* -- <id> )

	,&a STR2
	.length LDZ #00
	&loop
		DUP #0a MUL .windows ADD #06 ADD LDZ2 [ LIT2 &a $2 ] EQU2 ,&found JCN
		INC GTHk ,&loop JCN
	POP2
	#ff

JMP2r
	&found NIP JMP2r

@add-err ( action* target* -- )

	;app-error/target STA2
	;app-error #1809 #0000 DUP2 ;add-win JSR2
	;center-win JSR2

JMP2r

@add-form ( action* target* callback* -- )

	;app-form/callback STA2
	;buf/form ;scpy JSR2
	DUP2 ;app-form/action STA2
		;app-form #1808 #0000 DUP2 ;add-win JSR2
	;center-win JSR2

JMP2r

@add-option ( action* target* callback* -- )

	;app-option/callback STA2
	;app-option/target STA2
	DUP2 ;app-option/action STA2
		;app-option #1807 #0000 DUP2 ;add-win JSR2
	;center-win JSR2

JMP2r

@add-win ( name* app* h w y* x* -- )

	.length LDZ #0a MUL .windows ADD STHk
	STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHkr STZ2 INCr INCr
	STHr STZ2
	.length LDZk INC SWP STZ
	.length LDZ #01 SUB

@sel-win ( id -- )

	DUP #ff EQU ;sel-desktop JCN2
	DUP .length LDZ GTH ,&unchanged JCN

	;get-win JSR2 #000a SWP2 ;get-active-win JSR2 ;mswp JSR2

	( draw menu )
	;get-active-win JSR2
		DUP2 ;draw-win JSR2
		#0006 ADD2 LDA2

	( get manifest )
	LDA2 ;draw-menu/manifest STA2

	;draw-menu-bg JSR2
	;draw-menu JSR2

JMP2r
	&unchanged POP JMP2r

@go-active ( -- )

	.length LDZ
	DUP ,&valid JCN
	POP

JMP2r
	&valid #01 SUB ,sel-win JSR JMP2r

@go-home ( -- )

	;dict/parent-ext ;push-dir JSR2

JMP2r

@go-file ( -- )

	;draw-item-text/sel LDA ;get-file JSR2 ;open-file JSR2

JMP2r

@sel-desktop ( -- )

	;manifest ;draw-menu/manifest LDA2 EQU2 ,&skip JCN
		;manifest ;draw-menu/manifest STA2
		;draw-menu-bg JSR2
		;draw-menu JSR2
		&skip

JMP2r

@close-win ( -- )

	.length LDZ ,&continue JCN
		JMP2r
		&continue
	.length LDZk #01 SUB SWP STZ
	;sel-desktop JSR2
	;draw-desktop JSR2

JMP2r

@tab-win ( -- )

	.Screen/height DEI2 #000a SUB2 ;get-active-win JSR2 INC2 INC2 STA2
	;sel-desktop JSR2
	;draw-desktop JSR2

JMP2r

@expand-win ( -- )

	#0018 ;get-active-win JSR2 INC2 INC2 STA2
	;draw-desktop JSR2

JMP2r

@center-win ( -- )

	;get-active-win JSR2
	STH2k
	( /w ) #0004 ADD2
	( -x ) LDAk #00 SWP #31 SFT2 .Screen/width DEI2 #01 SFT2 SWP2 SUB2 STH2kr STA2
	( /h ) INC2 INC2r INC2r
	( -y ) LDA #00 SWP #31 SFT2 .Screen/height DEI2 #01 SFT2 SWP2 SUB2 STH2r STA2
	;draw-desktop JSR2

JMP2r

@get-rows ( -- rows )

	.Screen/height DEI2 #0018 SUB2 #0018 DIV2 NIP

JMP2r

@get-active-win ( -- addr* )

	.length LDZ #01 SUB

@get-win ( id -- addr* )

	DUP #ff NEQ ,&exists JCN
		POP #ffff JMP2r
		&exists
	#0a MUL .windows ADD #00 SWP

JMP2r

@pick-icon ( x* y* -- <id> )

	( y ) #0018 SUB2 #0018 DIV2 NIP
	ROT ROT
	( x ) #0010 SUB2 #00a0 DIV2 NIP
	;get-rows JSR2 MUL ADD

JMP2r

@sel-up ( -- )

	;draw-item-text/sel LDA
		DUP ,&continue JCN POP JMP2r
		&continue #01 SUB ;sel-icon JSR2

JMP2r

@sel-down ( -- )

	;draw-item-text/sel LDA INC ;sel-icon JSR2

JMP2r

@sel-left ( -- )

	;draw-item-text/sel LDA ;get-rows JSR2 SUB ;sel-icon JSR2

JMP2r

@sel-right ( -- )

	;draw-item-text/sel LDA ;get-rows JSR2 ADD ;sel-icon JSR2

JMP2r

@sel-icon ( id -- )

	;draw-item-text/sel STA
	;draw-desktop JSR2

JMP2r

@desk-len ( -- len )

	LITr 00
	;buf/dir
	&w
		;scap JSR2 INCr
		INC2 LDAk ,&w JCN
	POP2
	STHr
	#01 SUB

JMP2r

@get-file ( id -- <file*> )

	STH
	LITr 00
	;buf/dir
	&w
		EQUkr STHr ,&found JCN
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POP2r
	#ffff

JMP2r
	&found POP2r JMP2r

@find-name ( name* -- <id> )

	,&t STR2
	LITr 00
	;buf/dir
	&w
		#0005 ADD2 DUP2 [ LIT2 &t $2 ] ;scmp JSR2 ,&found JCN
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POPr
	#ff

JMP2r
	&found POP2 STHr JMP2r

@load-dir ( -- )

	;buf/loc .File/name DEO2
	#0400 .File/length DEO2
	;buf/dir
		DUP2 .File/read DEO2
	( remove linebreaks )
	&w
		LDAk #0a NEQ ,&no-lb JCN
			STH2k #00 STH2r STA
			&no-lb
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@draw-home ( -- )

	,load-dir JSR
	;get-rows JSR2 STH
	LITr 00
	;buf/dir
	&w
		#00 STHk2r SWP DIV #00a0 MUL2 #0010 ADD2 .Screen/x DEO2
		#00 STHk2r SWP MOD #0018 MUL2 #0018 ADD2 .Screen/y DEO2
		DUP2 ;draw-item-icon JSR2
		DUP2 STHkr ;draw-item-text JSR2
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POP2r

JMP2r

@draw-icon-size ( size* name* icon* -- )

	;draw-icon JSR2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2
	.Screen/x DEI2k STH2k #0020 ADD2 ROT DEO2
	[ LIT &c1 0a ] ;draw-chr/color STA
	;draw-text JSR2
	STH2r #0020 ADD2 .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2
	[ LIT &c2 05 ] ;draw-chr/color STA
	;draw-dec JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

JMP2r

@draw-item-icon ( ptr* -- ptr* )

	LDAk LIT '- EQU ,&folder JCN
	LDAk LIT '? EQU ,&unknown JCN
	DUP2 #0005 ADD2 LDA LIT '. EQU ,&unknown JCN
	DUP2 #0005 ADD2 ;dict/rom-ext ;has-ext JSR2 ,&rom JCN
	DUP2 #0005 ADD2 ;dict/chr-ext ;has-ext JSR2 ,&picture JCN
	DUP2 #0005 ADD2 ;dict/icn-ext ;has-ext JSR2 ,&picture JCN
	DUP2 #0005 ADD2 ;dict/pcm-ext ;has-ext JSR2 ,&sample JCN
	DUP2 #0005 ADD2 ;dict/uf2-ext ;has-ext JSR2 ,&font JCN
	POP2 ;icons/text ;draw-icon JSR2

JMP2r
	&folder POP2 ;icons/folder ;draw-icon JSR2 JMP2r
	&unknown POP2 ;icons/unknown ;draw-icon JSR2 JMP2r
	&picture POP2 ;icons/picture ;draw-icon JSR2 JMP2r
	&rom POP2 ;icons/application ;draw-icon JSR2 JMP2r
	&sample POP2 ;icons/sample ;draw-icon JSR2 JMP2r
	&font POP2 ;icons/font ;draw-icon JSR2 JMP2r

@draw-item-text ( ptr* id -- ptr* )

	[ LIT &sel 00 ] EQU #09 MUL INC INC ;draw-chr/color STA

	LDAk LIT '- EQU ;&no-size JCN2
	LDAk LIT '? EQU ;&no-size JCN2

	.Screen/x DEI2k #0020 ADD2 STH2k ROT DEO2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2

	#0005 ADD2 ;draw-text JSR2
	STH2r .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2

	#01 ;draw-chr/color STA
	DUP2 ;&buf #0004 ;mcpy JSR2
	;&buf ;shex JSR2 ;draw-dec JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

JMP2r
	&buf $5
	&no-size
		.Screen/y DEI2k #0010 SUB2 ROT DEO2
		.Screen/x DEI2k #0020 ADD2 ROT DEO2
		#0005 ADD2 ;draw-text JSR2
	JMP2r
