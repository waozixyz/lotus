@size-desktop-start

@manifest

	04 "Potato $1
		01 "1 =open-about "About $1
		01 "t =open-tile "Wallpaper $1
		01 "k =open-color "Theme $1
		01 "q =exit "Exit $1
	04 "File $1
		01 "n =file-create "Create $1
		01 "r =file-rename "Rename $1
		01 "d =file-clone "Clone $1
		01 08 =file-delete "Delete $1
	05 "Open $1
		05 "D =open-as-meta "As 20 "Meta $1
		05 "T =open-as-text "As 20 "Text $1
		05 "P =open-as-pict "As 20 "Pict $1
		05 "F =open-as-font "As 20 "Font $1
		05 "S =open-as-sound "As 20 "Sound $1
	04 "Select $1
		10 00 =sel-up "Up $1
		20 00 =sel-down "Down $1
		40 00 =sel-left "Left $1
		80 00 =sel-right "Right $1
	03 "Go $1
		12 00 =go-active "Active $1
		42 00 =go-home "Home $1
		82 00 =go-file "Open $1
	$1

(
@|vectors )

@untrap ( -- )

	;on-mouse .Mouse/vector DEO2
	;on-button .Controller/vector DEO2
	;on-frame .Screen/vector DEO2
	( release mouse ) #00 .Mouse/state DEO

JMP2r

@on-frame ( -> )

	;update-clock JSR2

BRK

@on-button ( -> )

	.Controller/button DEI2 ;find-modkey JSR2
	ORAk #00 EQU ,&skip JCN
		( blocking ) JSR2 BRK
		&skip
	POP2

	( send button to active win )
	( win ) ;get-active-win JSR2
	DUP2 #ffff EQU2 ,&no-win JCN
		.Controller/button DEI2 SWP2 DUP2
		( win/app ) #0006 ADD2 LDA2
		( win/app/on-button ) #0008 ADD2 LDA2 JMP2
	&no-win
	POP2

BRK

@on-mouse ( -> )

	.Mouse/y DEI2 #000c LTH2 ;trap-menu JCN2

	#42 .Mouse/state DEI #00 NEQ SUB ;cursor-icn ;update-cursor JSR2

	( handle drag )
	.drag LDZ #ff NEQ ;on-drag JCN2
	.Mouse/state DEI .Controller/button DEI #0102 EQU2 ;on-drag-start JCN2

	( pick window )
	.Mouse/state DEI #00 EQU ,&no-touch-win JCN
		.Mouse/x DEI2 .Mouse/y DEI2 ;pick-win JSR2
		DUP #ff EQU ,touch-desktop JCN
		DUP ;sel-win JSR2
		( pick dragbar )
		( win ) ;get-win JSR2
		( win/y ) INC2 INC2 LDA2
			.Mouse/y DEI2 SWP2 SUB2 #000a GTH2 ,&no-bar JCN
			;set-drag JSR2
			&no-bar
		&no-touch-win

	( send mouse to active win )
	.Mouse/x DEI2 .Mouse/y DEI2 ;get-active-win JSR2
		DUP2 #ffff EQU2 ,&skip JCN
		ROT2k ROT2 ROT2 ;within-win JSR2 ,&send JCN
	&skip

	POP2 POP2 POP2

BRK
	&send
		;rel-mouse JSR2
		( win ) ;get-active-win JSR2
		( win/app ) #0006 ADD2 LDA2
		( win/app/on-mouse ) #0006 ADD2 LDA2 JMP2

@touch-desktop ( win -> )

	POP
	.Mouse/x DEI2 .Mouse/y DEI2 ;pick-icon JSR2 ;sel-icon JSR2
	.Mouse/state DEI #02 LTH ,&no-launch JCN
		;go-file JSR2
		&no-launch
	#00 .Mouse/state DEO

BRK

( drag )

@on-drag ( -> )

	.Mouse/state DEI #00 EQU ;on-drag-end JCN2

	.drag LDZ ;get-win JSR2 STH2k

	( clear )
	DUP2 #40 ;draw-drag JSR2

	( move window )
	#0008 .Mouse/x DEI2 [ LIT2 &x $2 ] SUB2 STH2kr LDA2 ADD2
		;clamp-win JSR2 STH2kr STA2
	INC2r INC2r
	#0018 .Mouse/y DEI2 [ LIT2 &y $2 ] SUB2 STH2kr LDA2 ADD2
		;clamp-win JSR2
		.Screen/height DEI2 #000a SUB2 LTH2k [ JMP SWP2 POP2 ]
		STH2r STA2

	( update )
	.Mouse/x DEI2 .Mouse/y DEI2
		;on-drag/y STA2 ;on-drag/x STA2

	( draw )
	#cf ;draw-drag JSR2

BRK

@clamp-win ( x* -- x* )

	DUP2 ROT2 STH2k SUB2 #8000 GTH2 ,&gth JCN
	POP2r

JMP2r
	&gth POP2 STH2r JMP2r

@on-drag-end ( -> )

	( clear )
	.drag LDZ ;get-win JSR2 #40 ;draw-drag JSR2
	#ff .drag STZ
	;draw-desktop JSR2

BRK

@on-drag-start ( -> )

	.Mouse/x DEI2 .Mouse/y DEI2 ;pick-win JSR2 DUP #ff EQU ,&no-win JCN
		;sel-win JSR2 ;set-drag JSR2

BRK
	&no-win BRK

@set-drag ( -- )

	.length LDZ #01 SUB .drag STZ
	.Mouse/x DEI2 ;on-drag/x STA2
	.Mouse/y DEI2 ;on-drag/y STA2

JMP2r

(
@|window )

@get-active-win ( -- addr* )

	.length LDZ #01 SUB

@get-win ( id -- addr* )

	DUP #10 LTH ,&exists JCN
		POP #ffff JMP2r
		&exists
	#40 SFT #00 SWP ;windows ADD2

JMP2r

@pick-win ( x* y* -- id|ff )

	,&y STR2 ,&x STR2
	#00 .length LDZ DUP #00 EQU ,&skip JCN
	&loop
		DUP #01 SUB ;get-win JSR2 [ LIT2 &x $2 ] [ LIT2 &y $2 ] ROT2 ,within-win JSR ,&found JCN
		#01 SUB LTHk ,&loop JCN
		&skip
	POP2
	#ff

JMP2r
	&found NIP #01 SUB JMP2r

@within-win ( x* y* win* -- flag )

	STH2
	( x0 ) OVR2 STH2kr LDA2 LTH2 ,&fail JCN
	( y0 ) DUP2 STH2kr INC2 INC2 LDA2 LTH2 ,&fail JCN
	( x1 ) OVR2 STH2kr LDA2k SWP2 #0004 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTH2 ,&fail JCN
	( y1 ) DUP2 STH2kr INC2 INC2 LDA2k SWP2 #0003 ADD2 LDA #00 SWP #30 SFT2 ADD2 GTH2 ,&fail JCN
	POP2 POP2 POP2r #01

JMP2r
	&fail POP2 POP2 POP2r #00 JMP2r

@find-win ( app* -- id|ff )

	,&a STR2
	.length LDZ #00
	&loop
		( win ) DUP ;get-win JSR2
		( win/app ) #0006 ADD2 LDA2 [ LIT2 &a $2 ] EQU2 ,&found JCN
		INC GTHk ,&loop JCN
	POP2
	#ff

JMP2r
	&found NIP JMP2r

(
@|modals )

@add-err ( action* target* -- )

	;app-error/target STA2
	;app-error #1809 #0000 DUP2 ;add-win JSR2
	;center-win ( .. )

JMP2

@add-form ( action* target* callback* -- )

	;app-form/callback STA2
	;buf/form ;scpy JSR2
	DUP2 ;app-form/action STA2
		;app-form #1808 #0000 DUP2 ;add-win JSR2
	;center-win ( .. )

JMP2

@add-option ( action* target* callback* -- )

	;app-option/callback STA2
	;app-option/target STA2
	DUP2 ;app-option/action STA2
		;app-option #1807 #0000 DUP2 ;add-win JSR2
	;center-win ( .. )

JMP2

@add-win ( name* app* h w y* x* -- )

	.length LDZ #40 SFT #00 SWP ;windows ADD2 STH2k
	STA2 INCr INCr
	STH2kr STA2 INCr INCr
	STH2kr STA2 INCr INCr
	STH2kr STA2 INCr INCr
	STH2r STA2

	( init vector )
	.length LDZ ;get-win JSR2
		( win/app ) DUP2 #0006 ADD2 LDA2
		( win/app/on-init ) INC2 INC2 LDA2 JSR2

	.length LDZk INC SWP STZ
	.length LDZ #01 SUB

@sel-win ( id -- )

	DUP #ff EQU ,&desktop JCN
	DUP .sel/win LDZ EQU ,&unchanged JCN

	DUP ;get-win JSR2 #0010 SWP2 ;get-active-win JSR2 ;mswp JSR2

	( draw menu )
	;get-active-win JSR2
		DUP2 ;draw-win JSR2
		#0006 ADD2 LDA2

	( get manifest )
	LDA2 ;draw-menu/manifest STA2

	;draw-menu-bg JSR2
	;draw-menu JSR2
	.sel/win STZ

JMP2r
	&desktop POP ;sel-desktop JMP2
	&unchanged POP JMP2r

@close-win ( -- )

	.length LDZ ,&continue JCN
		( nothing to close ) JMP2r
		&continue

	;get-active-win JSR2
		( win/mem-ptr ) #000a ADD2 LDA2k SWP2
		( win/mem-len ) INC2 INC2 LDA2 ;mem-rel JSR2

	#00 .length LDZ #01 SUB #40 SFT2 ;windows ADD2 #0010 ;mclr JSR2

	.length LDZk #01 SUB SWP STZ
	;sel-desktop JSR2
	;draw-desktop ( .. )

JMP2

@sel-desktop ( -- )

	.sel/win LDZ #ff EQU ,&skip JCN
		;manifest ;draw-menu/manifest STA2
		;draw-menu-bg JSR2
		;draw-menu JSR2
		#ff .sel/win STZ
		&skip

JMP2r

@tab-win ( -- )

	.Screen/height DEI2 #000a SUB2
		( win/y ) ;get-active-win JSR2 INC2 INC2 STA2
	;sel-desktop JSR2
	;draw-desktop ( .. )

JMP2

@expand-win ( -- )

	#0018
		( win/y ) ;get-active-win JSR2 INC2 INC2 STA2
	;draw-desktop ( .. )

JMP2

@center-win ( -- )

	;get-active-win JSR2
	STH2k
	( /w ) #0004 ADD2
	( -x ) LDAk #00 SWP #31 SFT2 .Screen/width DEI2 #01 SFT2 SWP2 SUB2 STH2kr STA2
	( /h ) INC2 INC2r INC2r
	( -y ) LDA #00 SWP #31 SFT2 .Screen/height DEI2 #01 SFT2 SWP2 SUB2 STH2r STA2
	;draw-desktop ( .. )

JMP2

(
@|open-as )

@open-as-meta ( -- )

	;open-meta ;open-as JMP2

@open-as-text ( -- )

	;open-text ,open-as JMP

@open-as-pict ( -- )

	;open-pict ,open-as JMP

@open-as-font ( -- )

	;open-font ,open-as JMP

@open-as-sound ( -- )

	;open-sound ,open-as JMP

@open-as ( routine* -- )

	;get-sel-file JSR2

	LDAk LIT "- EQU ,&invalid JCN
	LDAk LIT "? EQU ,&invalid JCN

	( run routine ) SWP2 JSR2

	;center-win ( .. )

JMP2
	&invalid NIP2 #0005 ADD2 ;dict/open SWP2 ;add-err JMP2

(
@|go )

@go-active ( -- )

	.length LDZ
	DUP ,&valid JCN
	POP

JMP2r
	&valid #01 SUB ;sel-win JMP2

@go-home ( -- )

	;dict/parent-ext ;push-dir ( .. )

JMP2

@go-file ( -- )

	;get-sel-file JSR2 ;open-file ( .. )

JMP2

(
@|icon-picking )

@pick-icon ( x* y* -- id )

	( y ) #0018 SUB2 #0018 DIV2 NIP
	ROT ROT
	( x ) #0010 SUB2 #00a0 DIV2 NIP
	,get-rows JSR MUL ADD

JMP2r

@get-rows ( -- rows )

	.Screen/height DEI2 #0018 SUB2 #0018 DIV2 NIP

JMP2r

@sel-left ( -- ) ;draw-item-text/sel LDA ,get-rows JSR SUB ,sel-icon JMP
@sel-right ( -- ) ;draw-item-text/sel LDA ,get-rows JSR ADD ,sel-icon JMP
@sel-up ( -- ) ;draw-item-text/sel LDA #01 SUB ,sel-icon JMP
@sel-down ( -- ) ;draw-item-text/sel LDA INC ,sel-icon JMP

@sel-icon ( id -- )

	( min ) DUP #80 LTH ,&min-ok JCN POP #00 &min-ok
	( max ) ,desk-len JSR LTHk [ JMP SWP POP ]
	;draw-item-text/sel STA
	;draw-desktop JSR2
	;sel-desktop ( .. )

JMP2

@desk-len ( -- len )

	LITr 00
	;buf/dir
	&w
		;scap JSR2 INCr
		INC2 LDAk ,&w JCN
	POP2
	STHr #01 SUB

JMP2r

@get-sel-file ( -- file*|ffff )

	;draw-item-text/sel LDA

@get-file ( id -- file*|ffff )

	STH
	LITr 00
	;buf/dir
	&w
		EQUkr STHr ,&found JCN
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POP2r
	#ffff

JMP2r
	&found POP2r JMP2r

@find-name ( name* -- id|ff )

	,&t STR2
	LITr 00
	;buf/dir
	&w
		#0005 ADD2
			DUP2 [ LIT2 &t $2 ] ;scmp JSR2 ,&found JCN
		;scap JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POPr
	#ff

JMP2r
	&found POP2 STHr JMP2r

@update-clock ( -- )

	[ LIT &last ff ] .DateTime/minute DEI NEQ ,&continue JCN
		JMP2r
		&continue

@draw-clock ( -- )

	#01 .Screen/auto DEO
	#0004 .Screen/y DEO2
	#0c ;draw-chr/color STA
	.Screen/width DEI2 #0098 SUB2 #00 .DateTime/day DEI #0a LTH #30 SFT2 ADD2 .Screen/x DEO2

	#00 .DateTime/dotw DEI #20 SFT ;dict/dotw ADD2 ;draw-text JSR2
	LIT ", ;draw-chr JSR2
	#20 ;draw-chr JSR2
	#00 .DateTime/day DEI ;draw-dec JSR2
	#20 ;draw-chr JSR2
	#00 .DateTime/month DEI #20 SFT ;dict/months ADD2 ;draw-text JSR2
	#20 ;draw-chr JSR2
	.DateTime/hour DEI ,&d JSR
	LIT ": ;draw-chr JSR2
	.DateTime/minute DEI
		DUP ;update-clock/last STA
		,&d ( .. )

JMP

	&d DUP #0a DIV ,&c JSR #0a ( MOD ) [ DIVk MUL SUB ]
	&c #30 ADD ;draw-chr JMP2

@size-desktop-end

~src/apps.tal
