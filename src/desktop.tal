@manifest

	02 "Potato $1
		01 '1 :open-disk "Disk 20 "Util. $1
		01 'q :exit "Exit $1
	04 "Edit $1
		01 'c :edit-copy "Copy $1
		01 'v :edit-paste "Paste $1
		01 'x :edit-cut "Cut $1
		00 08 :edit-erase "Erase $1
	01 "View $1
		02 00 :toggle-zoom "Zoom $1
	$1

@draw-home ( -- )

	( load dir )
	;&path .File/name DEO2
	#8000 .File/length DEO2
	;buffer .File/read DEO2

	;buffer ;remove-lb JSR2

	#0010 .Screen/x DEO2
	#0018 .Screen/y DEO2
	;draw-potato JSR2

	#0010 .Screen/x DEO2
	#0030 .Screen/y DEO2
	;draw-disk JSR2

	LITr 02
	;buffer
	&w
		#00 STHkr #03 SFT2 #00a0 MUL2 #0010 ADD2 .Screen/x DEO2
		#00 STHkr #0007 AND2 #0018 MUL2 #0018 ADD2 .Screen/y DEO2
		;draw-item JSR2
		INCr
		INC2 LDAk ,&w JCN
	POP2
	POPr

JMP2r
	&path ". $1

@draw-item ( ptr* -- ptr* )

	LDAk LIT '- EQU ;&draw-folder JCN2
	LDAk LIT '? EQU ;&draw-large JCN2
	DUP2 #0005 ADD2 LDA LIT '. EQU ;&draw-hidden JCN2

	DUP2 ;&buf #0004 ;mcpy JSR2

	;&buf OVR2 #0005 ADD2 ;text-chr ;draw-icon-size JSR2

	;scap JSR2

JMP2r
	&buf $5
	&bytes 20 "bytes $1

&draw-folder ( ptr* -- ptr* )

	;folder-chr ;draw-icon JSR2
	.Screen/y DEI2k #0010 SUB2 ROT DEO2
	.Screen/x DEI2k #0020 ADD2 ROT DEO2
	#0e ;draw-chr/color STA
	#0005 ADD2 DUP2 ;draw-str JSR2 POP2
	;scap JSR2

JMP2r

&draw-large ( ptr* -- ptr* )

	;icon-unknown ;draw-icon JSR2
	.Screen/y DEI2k #0010 SUB2 ROT DEO2
	.Screen/x DEI2k #0020 ADD2 ROT DEO2
	#0d ;draw-chr/color STA
	#0005 ADD2 DUP2 ;draw-str JSR2 POP2
	;scap JSR2

JMP2r

&draw-hidden ( ptr* -- ptr* )

	;pict-chr ;draw-icon JSR2
	.Screen/y DEI2k #0010 SUB2 ROT DEO2
	.Screen/x DEI2k #0020 ADD2 ROT DEO2
	#0d ;draw-chr/color STA
	#0005 ADD2 DUP2 ;draw-str JSR2 POP2
	;scap JSR2

JMP2r

@draw-potato ( -- )

	;icon-potato ;draw-icon JSR2
	.Screen/y DEI2k STH2k #0014 SUB2 ROT DEO2
	.Screen/x DEI2k #0020 ADD2 ROT DEO2
	#0b ;draw-chr/color STA
	;&name ;draw-str JSR2 POP2

	STH2r .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2

	#02 ;draw-chr/color STA
	#0000 ;buffer SUB2 ;draw-dec JSR2
	;&free ;draw-str JSR2 POP2

JMP2r
	&name "Potato $1
	&free 20 "Free $1

@draw-disk ( -- )

	;disk-chr ;draw-icon JSR2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2
	.Screen/x DEI2k STH2k #0020 ADD2 ROT DEO2
	#0b ;draw-chr/color STA
	;&name ;draw-str JSR2 POP2

	STH2r #0018 ADD2 .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2
	#02 ;draw-chr/color STA
	;&free ;draw-str JSR2 POP2

JMP2r
	&name "Disk 20 "Drive $1
	&free 20 "<Empty> $1

@remove-lb ( addr* -- )

	&w
		LDAk #0a NEQ ,&no-lb JCN
			STH2k #00 STH2r STA
			&no-lb
		INC2 LDAk ,&w JCN
	POP2

JMP2r

@draw-icon-size ( size* name* icon* -- )

	;draw-icon JSR2
	.Screen/y DEI2k #0014 SUB2 ROT DEO2
	.Screen/x DEI2k STH2k #0020 ADD2 ROT DEO2
	[ LIT &c1 0a ] ;draw-chr/color STA
	;draw-str JSR2 POP2
	STH2r #0020 ADD2 .Screen/x DEO2
	.Screen/y DEI2k #0009 ADD2 ROT DEO2
	[ LIT &c2 05 ] ;draw-chr/color STA
	;draw-dec JSR2
	;&bytes ;draw-str JSR2 POP2

JMP2r
	&bytes 20 "bytes $1

@edit-copy ( -- ) JMP2r
@edit-paste ( -- ) JMP2r
@edit-cut ( -- ) JMP2r
@edit-erase ( -- ) JMP2r
@toggle-zoom ( -- ) JMP2r
