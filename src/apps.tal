@disk-manifest

	03 "Disk $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	02 "Settings $1
		02 00 :toggle-zoom "Decimal $1 ( TODO )
		02 00 :toggle-zoom "Hexadecimal $1 ( TODO )
	$1

@app-disk
	:disk-manifest
	:&on-draw
	:void-mouse
	:void-button

&on-draw ( win* -- )

	#40 #0000 DUP2 ;buf/dir SUB2 #0100 SUB2 SUB2 #0400 DIV2 NIP ;draw-progbar JSR2

	DUP2
	LDA2k #0008 ADD2 .Screen/x DEO2 POP2
	.Screen/y DEI2k #000c ADD2 ROT DEO2
	;buf/dir ;draw-dec JSR2
	LIT '/ ;draw-chr JSR2
	;&full ;draw-text JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

	DUP2
	LDA2k .Screen/x DEO2 POP2
	.Screen/y DEI2k #000c ADD2 ROT DEO2
	#16 ;draw-dotted JSR2

	LDA2k #0008 ADD2 ,&x STR2 INC2 INC2
	LDA2k #0030 ADD2 ,&y STR2 POP2
	#0400
	&loop
		[ LIT2 &x $2 ] .Screen/x DEO2
		#00 OVR #0018 MUL2 [ LIT2 &y $2 ] ADD2 .Screen/y DEO2
		#00 OVR #30 SFT2 ;&items ADD2 STH2k
		LDA2 INC2r INC2r
		STH2kr LDA2 SUB2 INC2r INC2r
		STH2kr LDA2 INC2r INC2r
		STH2r LDA2 ;draw-icon-size JSR2
		INC GTHk ,&loop JCN
	POP2

JMP2r
	&items
		:size-assets-end :size-assets-start :dict/fontsicons :icons/fonts
		:size-desktop-end :size-desktop-start :dict/desktop :icons/desktop
		:size-apps-end :size-apps-start :dict/applications :icons/application
		:size-system-end :size-system-start :dict/system :icons/potato
	&full "65536 $1

( ------------------------------------------------------------- )

@color-manifest

	03 "Color $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	01 "File $1
		01 's :save-theme "Save $1
	04 "Select $1
		00 '1 :select-color1 "Background $1
		00 '2 :select-color2 "Color1 $1
		00 '3 :select-color3 "Color2 $1
		00 '4 :select-color4 "Color3 $1
	$1

@app-color
	:color-manifest
	:&on-draw
	:&on-mouse
	:void-button

&on-mouse ( x* y* win* -> )

	.Mouse/state DEI ,&on-click JCN
		POP2 POP2 POP2 BRK
		&on-click
	DUP2 ,&win STR2
	;rel-mouse JSR2 POP2
	#03 SFT2 NIP ROT ROT #02 SFT2 NIP SWP

	OVR #0f GTH ,&skip JCN
	DUP #01 NEQ ,&no-r JCN
		OVR .System/r STHk ,&set-color JSR STHr ,&set-nibble JSR
		&no-r
	DUP #04 NEQ ,&no-g JCN
		OVR .System/g STHk ,&set-color JSR STHr ,&set-nibble JSR
		&no-g
	DUP #07 NEQ ,&no-b JCN
		OVR .System/b STHk ,&set-color JSR STHr ,&set-nibble JSR
		&no-b
	DUP #09 NEQ ,&no-swatch JCN
		OVR #02 SFT .cursor/color STZ
		[ LIT2 &win $2 ] ;draw-win JSR2
		,&release JMP
		&no-swatch
		&skip
	POP2

BRK
	&release #00 .Mouse/state DEO POP2 BRK
	&set-nibble ( -- )
		.cursor/color LDZ #01 SFT ADD DEO
	JMP2r
	&set-color ( value color -- )
		SWP ,&v STR
		.cursor/color LDZ STHk #01 SFT ADD DEI
		STHr #01 AND STHk
		#0f SWP [ #60 SFT SFT ] AND
		STHr #00 EQU
		[ LIT &v $1 ]
		SWP [ #60 SFT SFT ] ADD
	JMP2r

&on-draw ( win* -- )

	POP2
	[ .System/r ,&get-color JSR ] ;dict/red
		;draw-slider JSR2
	[ .System/g ,&get-color JSR ] ;dict/green
		;draw-slider JSR2
	[ .System/b ,&get-color JSR ] ;dict/blue
		;draw-slider JSR2
	( swatches )
	;draw-swatches JSR2

	.System/r ,&get-color JSR ;draw-hex JSR2
	.System/g ,&get-color JSR ;draw-hex JSR2
	.System/b ,&get-color JSR ;draw-hex JSR2

JMP2r

&get-color

	.cursor/color LDZ STHk #01 SFT ADD DEI #01 STHr #01 AND SUB #20 SFT SFT #0f AND

JMP2r

@save-theme ( -- )

	;dict/theme-ext .File/name DEO2
	#0002 .File/length DEO2
	.System/r DEI2 ,&w JSR
	.System/g DEI2 ,&w JSR
	.System/b DEI2 ,&w JSR

JMP2r
	&w ,&b STR2 ;&b .File/write DEO2 JMP2r
	&b $2

( ------------------------------------------------------------- )

@tile-manifest

	03 "Tile $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	05 "Edit $1
		00 08 :clear-patt "Clear $1
		40 00 :prev-tile "Prev $1
		80 00 :next-tile "Next $1
		01 'c :copy-patt "Copy $1
		01 'v :paste-patt "Paste $1
	04 "Color $1
		00 '1 :select-color1 "Background $1
		00 '2 :select-color2 "Color1 $1
		00 '3 :select-color3 "Color2 $1
		00 '4 :select-color4 "Color3 $1
	02 "Mode $1
		00 00 :mode-chr "2-bit $1
		00 00 :mode-icn "1-bit $1
	$1

@app-tile
	:tile-manifest
	:&on-draw
	:&on-mouse
	:void-button

&on-draw ( win* -- )

	POP2

	.Screen/x DEI2 ,&x STR2
	.Screen/y DEI2 ,&y STR2
	;bigpixel-icn .Screen/addr DEO2
	#4000
	&v
		#00 OVR #07 AND #30 SFT2 [ LIT2 &x $2 ] ADD2 .Screen/x DEO2
		#00 OVR #33 SFT2 [ LIT2 &y $2 ] ADD2 .Screen/y DEO2
		DUP ;paint-patt/addr LDA2 ROT ;get-chr JSR2
		#8c ADD .Screen/sprite DEO
		INC GTHk ,&v JCN
	POP2

	,&x LDR2 .Screen/x DEO2
	.Screen/y DEI2k #0010 ADD2 ROT DEO2

	( swatches )
	;draw-swatches JSR2

	( addr )
	#01 .Screen/auto DEO
	;arrow-icns/h .Screen/addr DEO2
	#0a .Screen/sprite DEO
	#0f .Screen/sprite DEO
	;paint-patt/addr LDA2 ;draw-short JSR2
	#0f .Screen/sprite DEO
	;arrow-icns/h .Screen/addr DEO2
	#1a .Screen/sprite DEO

JMP2r

&on-mouse ( x* y* win* -> )

	.Mouse/state DEI ,&on-click JCN
		POP2 POP2 POP2 BRK
		&on-click
	;rel-mouse JSR2 POP2
	#03 SFT2 NIP ROT ROT #03 SFT2 NIP SWP
	OVR #07 GTH ,&skip JCN
	DUP #07 GTH ,&no-paint JCN
		;paint-patt JSR2 BRK
		&no-paint
	DUP #09 NEQ ,&no-swatch JCN
		OVR #01 SFT .cursor/color STZ
		#ffff ;paint-patt/last STA2
		,&release JMP
		&no-swatch
	DUP2 #000b NEQ2 ,&no-l JCN
		;prev-tile JSR2
		,&release JMP
		&no-l
	DUP2 #070b NEQ2 ,&no-r JCN
		;next-tile JSR2
		,&release JMP
		&no-r
	&skip
	POP2

BRK
	&release #00 .Mouse/state DEO POP2 BRK

@select-color1 ( -- ) #00 .cursor/color STZ JMP2r
@select-color2 ( -- ) #01 .cursor/color STZ JMP2r
@select-color3 ( -- ) #02 .cursor/color STZ JMP2r
@select-color4 ( -- ) #03 .cursor/color STZ JMP2r

@mode-chr ( -- ) ( TODO ) JMP2r
@mode-icn ( -- ) ( TODO ) JMP2r

@copy-patt ( -- )

	;dict/snarf-ext .File/name DEO2
	#0010 .File/length DEO2
	;patt-chr .File/write DEO2

JMP2r

@paste-patt ( -- )

	;dict/snarf-ext .File/name DEO2
	#0010 .File/length DEO2
	;patt-chr .File/read DEO2
	;draw-desktop JSR2

JMP2r

@prev-tile ( -- )

	;paint-patt/addr LDA2 #0010 SUB2 ,sel-tile JSR

JMP2r

@next-tile ( -- )

	;paint-patt/addr LDA2 #0010 ADD2

@sel-tile ( addr* -- )

	;paint-patt/addr STA2 ;draw-desktop JSR2

JMP2r

@clear-patt ( -- )

	;paint-patt/addr LDA2 #0010 ;mclr JSR2 ;draw-desktop JSR2

JMP2r

@paint-patt ( x y -- )

	DUP2 [ LIT2 &last ffff ] NEQ2 ,&changed JCN
		POP2 JMP2r
		&changed

	DUP2 ,&last STR2

	#00 SWP [ LIT2 &addr :patt-chr ] ADD2
	ROT #00 SWP
	SWP2
	OVR2 OVR2
	( ch1 ) .cursor/color LDZ #00 ,toggle-pixel JSR
	( ch2 ) #0008 ADD2 .cursor/color LDZ #01 ,toggle-pixel JSR
	;draw-desktop JSR2

JMP2r

@toggle-pixel ( x* addr* color index -- )

	STH2
	LDAk
	STH SWP2 NIP
	STHr SWP
	STH2r SFT #01 AND ,&do-set JCN
		( mask ) #01 #07 ROT #07 AND SUB #40 SFT SFT #ff EOR AND
		( save ) ROT ROT STA
		JMP2r
	&do-set
		( mask ) #01 #07 ROT #07 AND SUB #40 SFT SFT ORA
		( save ) ROT ROT STA

JMP2r

( ------------------------------------------------------------- )

@text-manifest

	03 "Text $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	04 "Edit $1
		01 'c :edit-copy "Copy $1
		01 'v :edit-paste "Paste $1
		01 'x :edit-cut "Cut $1
		00 08 :edit-erase "Erase $1
	01 "View $1
		02 00 :toggle-zoom "Zoom $1
	$1

@app-text
	:text-manifest
	:&on-draw
	:void-mouse
	:void-button

&on-draw ( win* -- )

	( get path )
	DUP2
	#0008 ADD2 LDA2 ;get-path JSR2 .File/name DEO2
	#0000 ;buf/app SUB2 .File/length DEO2
	;buf/app .File/read DEO2
	( cap )
	#00 .File/success DEI2 ;buf/app ADD2 STA

	DUP2 #0004 ADD2 LDA2
		#05 SUB SWP #02 SUB SWP

	;buf/app SWP2 ;draw-textarea JSR2

	POP2

JMP2r

( ------------------------------------------------------------- )

@pict-manifest

	03 "Picture $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	01 "View $1
		01 'i :app-pict/invert "Invert $1
	$1

@app-pict
	:pict-manifest
	:&on-draw
	:void-mouse
	:void-button

&on-draw ( win* -- )

	( no padding )
	.Screen/x DEI2k #0008 SUB2 ROT DEO2
	.Screen/y DEI2k #0002 SUB2 ROT DEO2

	#0004 ADD2 LDA2k SWP2 #0004 ADD2 LDA2 ;get-path JSR2 SWP2 ;draw-image JSR2

JMP2r

&invert ( -- )

	#0e #0b ;draw-image/color LDA #0e NEQ POP? ;draw-image/color STA

	;get-active-win JSR2
		DUP2 ;set-anchor-body JSR2 POP2
	;app-pict/on-draw JSR2

JMP2r

( --MODALS----------------------------------------------------- )

@form-manifest

	01 "Form $1
		42 00 :close-win "Cancel $1
	$1

@app-form
	:form-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-draw ( win* -- )

	POP2
	[ LIT2 &target $2 ] #14 ;draw-input JSR2
	.Screen/x DEI2k #0030 ADD2 ROT DEO2
	[ LIT2 &action $2 ] #08 ;draw-button JSR2

JMP2r

&on-mouse ( x* y* win* -- )

	.Mouse/state DEI ,&on-click JCN
		POP2 POP2 POP2 BRK
		&on-click
	;rel-mouse JSR2 POP2
	( focus button )
	#0018 SUB2 SWP2 #0030 SUB2 SWP2
	( collision )
	#000f LTH2 ROT ROT #0050 LTH2 AND ,&validate-mouse JCN

BRK
	&validate-mouse ( -- ) #00 .Mouse/state DEO ,&validate JMP JMP2r

&on-button ( button key win* -> )

	SWP2 NIP DUP ,&has-key JCN
		POP POP2 BRK
		&has-key
	DUP #0d EQU ,&validate-key JCN
	( handle key )
	;&target LDA2 #0040 ;skey JSR2
	;draw-win JSR2

BRK
	&validate-key ( key win* -- ) POP POP2

&validate ( win* key -> )

	#00 .Mouse/state DEO
	#0000 .Controller/button DEO2
	[ LIT2 &callback $2 ] JSR2

BRK

( ------------------------------------------------------------- )

@option-manifest

	01 "Option $1
		42 00 :close-win "Cancel $1
	$1

@app-option
	:option-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-draw ( win* -- )

	POP2
	[ LIT2 &target $2 ] ;draw-line JSR2
	;draw-lb JSR2
	.Screen/x DEI2k #0030 ADD2 ROT DEO2
	[ LIT2 &action $2 ] #08 ;draw-button JSR2

JMP2r

&on-mouse ( x* y* win* -- )

	.Mouse/state DEI ,&on-click JCN
		POP2 POP2 POP2 BRK
		&on-click
	;rel-mouse JSR2 POP2
	( focus button )
	#0010 SUB2 SWP2 #0030 SUB2 SWP2
	( collision )
	#000f LTH2 ROT ROT #0050 LTH2 AND ,&validate JCN

BRK

&on-button ( button key win* -> )

	POP2 NIP #0d EQU ,&validate JCN

BRK

&validate ( win* -> )

	#00 .Mouse/state DEO
	#0000 .Controller/button DEO2
	[ LIT2 &callback $2 ] JSR2

BRK

( ------------------------------------------------------------- )

@error-manifest

	01 "Error $1
		42 00 :close-win "Cancel $1
	$1

@app-error
	:error-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-draw ( win* -- )

	POP2
	;&invalid-txt ;draw-line JSR2
	;draw-lb JSR2
	[ LIT2 &target $2 ] ;draw-line JSR2
	;draw-lb JSR2
	.Screen/x DEI2k #0030 ADD2 ROT DEO2
	;dict/ok #08 ;draw-button JSR2

JMP2r
	&invalid-txt "Invalid 20 "Target: $1

&on-mouse ( x* y* win* -- )

	.Mouse/state DEI ,&on-click JCN
		POP2 POP2 POP2 BRK
		&on-click
	;rel-mouse JSR2 POP2
	( focus button )
	#0020 SUB2 SWP2 #0030 SUB2 SWP2
	( collision )
	#000f LTH2 ROT ROT #0050 LTH2 AND ,&validate JCN

BRK

&on-button ( button key win* -> )

	POP2 NIP ,&validate JCN

BRK

&validate ( win* -> )

	#00 .Mouse/state DEO
	#0000 .Controller/button DEO2
	;close-win JSR2

BRK

( ------------------------------------------------------------- )

@update-clock ( -- )

	[ LIT &last ff ] .DateTime/minute DEI NEQ ,&continue JCN
		JMP2r
		&continue

@draw-clock ( -- )

	#01 .Screen/auto DEO
	#0004 .Screen/y DEO2
	#0c ;draw-chr/color STA
	.Screen/width DEI2 #0098 SUB2 .Screen/x DEO2

	#00 .DateTime/dotw DEI #20 SFT ;dict/dotw ADD2 ;draw-text JSR2
	LIT ', ;draw-chr JSR2
	#20 ;draw-chr JSR2
	#00 .DateTime/day DEI ;draw-dec JSR2
	#20 ;draw-chr JSR2
	#00 .DateTime/month DEI #20 SFT ;dict/months ADD2 ;draw-text JSR2
	#20 ;draw-chr JSR2
	.DateTime/hour DEI ,&d JSR
	LIT ': ;draw-chr JSR2
	.DateTime/minute DEI
		DUP ;update-clock/last STA
		,&d JSR

JMP2r
	&d DUP #0a DIV ,&c JSR #0a MOD
	&c #30 ADD ;draw-chr JSR2 JMP2r

( ------------------------------------------------------------- )

( defaults )

@void-mouse ( x* y* win* -- ) POP2 POP2 POP2 BRK
@void-button ( button key win* -- ) POP2 POP2 BRK

@edit-copy ( -- ) JMP2r
@edit-paste ( -- ) JMP2r
@edit-cut ( -- ) JMP2r
@edit-erase ( -- ) JMP2r
@toggle-zoom ( -- ) JMP2r
