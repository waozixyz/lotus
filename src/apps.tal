@disk-manifest

	03 "Disk $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	02 "Settings $1
		02 00 :toggle-zoom "Decimal $1 ( TODO )
		02 00 :toggle-zoom "Hexadecimal $1 ( TODO )
	$1

@app-disk
	:disk-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-draw ( win* -- )

	#40 #0000 DUP2 ;buf/dir SUB2 #0100 SUB2 SUB2 #0400 DIV2 NIP ;draw-bar JSR2

	DUP2
	LDA2k #0008 ADD2 .Screen/x DEO2 POP2
	.Screen/y DEI2k #000c ADD2 ROT DEO2
	;buf/dir ;draw-dec JSR2
	LIT '/ ;draw-chr JSR2
	;&full ;draw-text JSR2
	#20 ;draw-chr JSR2
	;dict/bytes ;draw-text JSR2

	DUP2
	LDA2k .Screen/x DEO2 POP2
	.Screen/y DEI2k #000c ADD2 ROT DEO2
	#16 ;draw-dotted JSR2

	LDA2k #0008 ADD2 ,&x STR2 INC2 INC2
	LDA2k #0030 ADD2 ,&y STR2 POP2
	#0400
	&loop
		[ LIT2 &x $2 ] .Screen/x DEO2
		#00 OVR #0018 MUL2 [ LIT2 &y $2 ] ADD2 .Screen/y DEO2
		#00 OVR #30 SFT2 ;&items ADD2 STH2k
		LDA2 INC2r INC2r
		STH2kr LDA2 SUB2 INC2r INC2r
		STH2kr LDA2 INC2r INC2r
		STH2r LDA2 ;draw-icon-size JSR2
		INC GTHk ,&loop JCN
	POP2

JMP2r
	&items
		:size-assets-end :size-assets-start :dict/fontsicons :icons/fonts
		:size-desktop-end :size-desktop-start :dict/desktop :icons/desktop
		:size-apps-end :size-apps-start :dict/applications :icons/unknown
		:size-system-end :size-system-start :dict/system :icons/potato
	&full "65536 $1

&on-button ( win* -> ) POP2 BRK
&on-mouse ( win* -> ) POP2 BRK

( ------------------------------------------------------------- )

@look-manifest

	03 "Look $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	02 "Settings $1
		02 00 :toggle-zoom "Decimal $1 ( TODO )
		02 00 :toggle-zoom "Hexadecimal $1 ( TODO )
	$1

@app-look
	:look-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-draw ( win* -- )

	POP2
	;draw-editor JSR2

JMP2r

&on-button ( win* -> ) POP2 BRK
&on-mouse ( win* -> ) POP2 BRK

@draw-editor ( -- )

	.Screen/x DEI2 ,&x STR2
	.Screen/y DEI2 ,&y STR2
	;bigpixel-icn .Screen/addr DEO2
	#4000
	&v
		#00 OVR #07 AND #30 SFT2 [ LIT2 &x $2 ] ADD2 .Screen/x DEO2
		#00 OVR #33 SFT2 [ LIT2 &y $2 ] ADD2 .Screen/y DEO2
		#00 OVR #03 SFT2 ;patt-icn ADD2 LDA
		OVR #07 AND #07 SWP SUB SFT #01 AND #10 SFT
		#0c ADD .Screen/sprite DEO


		INC GTHk ,&v JCN
	POP2

JMP2r

( ------------------------------------------------------------- )

@text-manifest

	03 "Text $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	04 "Edit $1
		01 'c :edit-copy "Copy $1
		01 'v :edit-paste "Paste $1
		01 'x :edit-cut "Cut $1
		00 08 :edit-erase "Erase $1
	01 "View $1
		02 00 :toggle-zoom "Zoom $1
	$1

@app-text
	:text-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-button ( win* -> ) POP2 BRK
&on-mouse ( win* -> ) POP2 BRK

&on-draw ( win* -- )

	( get path )
	DUP2
	#0008 ADD2 LDA2 .File/name DEO2
	#0000 ;buf/app SUB2 .File/length DEO2
	;buf/app .File/read DEO2
	( cap )
	#00 .File/success DEI2 ;buf/app ADD2 STA

	DUP2 #0004 ADD2 LDA2
		#05 SUB SWP #02 SUB SWP

	;buf/app SWP2 ;draw-textarea JSR2

	POP2

JMP2r

( ------------------------------------------------------------- )

@pict-manifest

	03 "Picture $1
		12 00 :expand-win "Expand $1
		22 00 :tab-win "Tab $1
		42 00 :close-win "Close $1
	01 "View $1
		01 'i :app-pict-invert "Invert $1
	$1

@app-pict
	:pict-manifest
	:&on-draw
	:&on-mouse
	:&on-button

&on-button ( win* -> ) POP2 BRK
&on-mouse ( win* -> ) POP2 BRK

&on-draw ( win* -- )

	( no padding )
	.Screen/x DEI2k #0008 SUB2 ROT DEO2
	.Screen/y DEI2k #0002 SUB2 ROT DEO2

	#0004 ADD2 LDA2k SWP2 #0004 ADD2 LDA2 SWP2 ;draw-image JSR2

JMP2r

@app-pict-invert ( -- )

	#0e #0b ;draw-image/color LDA #0e NEQ POP? ;draw-image/color STA

	;get-active-win JSR2
		DUP2 ;set-anchor-body JSR2 POP2
	;app-pict/on-draw JSR2

JMP2r

( ------------------------------------------------------------- )

@update-clock ( -- )

	[ LIT &last ff ] .DateTime/minute DEI NEQ ,&continue JCN
		JMP2r
		&continue

@draw-clock ( -- )

	#01 .Screen/auto DEO
	#0004 .Screen/y DEO2
	#0c ;draw-chr/color STA
	.Screen/width DEI2 #0098 SUB2 .Screen/x DEO2

	#00 .DateTime/dotw DEI #20 SFT ;dict/dotw ADD2 ;draw-text JSR2
	LIT ', ;draw-chr JSR2
	#20 ;draw-chr JSR2
	#00 .DateTime/day DEI ;draw-dec JSR2
	#20 ;draw-chr JSR2
	#00 .DateTime/month DEI #20 SFT ;dict/months ADD2 ;draw-text JSR2
	#20 ;draw-chr JSR2
	.DateTime/hour DEI ,&d JSR
	LIT ': ;draw-chr JSR2
	.DateTime/minute DEI
		DUP ;update-clock/last STA
		,&d JSR

JMP2r
	&d DUP #0a DIV ,&c JSR #0a MOD
	&c #30 ADD ;draw-chr JSR2 JMP2r

( ------------------------------------------------------------- )
